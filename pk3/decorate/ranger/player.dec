actor QuakePlayer: PlayerPawn
{
    Radius 16
    Height 56
    Mass 100
    Scale 0.21
    Speed 1
    
    Gravity 0.75
    Player.JumpZ 6.9282
    
    Health 100
    PainChance 255
    
    Species "Player"
    -THRUSPECIES
    -DONTBLAST
    
    Player.SoundClass "ranger"
    Player.Face "RNG"
    Player.DisplayName "Ranger"
    //Player.CrouchSprite "PLYC"
    
    Player.StartItem "QuakeClass"
    Player.StartItem "Single Shotgun"
    Player.StartItem "IsSamsaraClass", 1
    Player.StartItem "Shell", 25
    Player.StartItem "Axe"
    
    Player.ColorRange 112, 127
    
    BounceFactor 0.35
    WallBounceFactor 0.5
    
    DamageFactor "PoisonCloud", 1.75
    //DamageFactor "Wraithverge", 0.75
    DamageFactor "WMCBeam", 0.75
    DamageFactor "WMCNade", 0.75
    DamageFactor "MightyBoot", 1.5
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.1

    DamageFactor "EnemyInfightDamage", 0.0
    
    BloodType "SamsaraBloodyHellBlood"
    
    States
    {
      Spawn:
        RANG A -1
        stop
        
      See:
        RANG BBBBCCCCDDDDEEEE 1
        Loop
        
      Missile:
        RANG G 0 A_JumpIfInventory("UsingLG", 1, "Missile2")
        RANG G 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound1")
        goto Missile2
        
      QuadSound1:
        RANG G 0 A_PlaySound("quakeweps/quadfire", 7)
        goto Missile2
        
      Missile2:
        RANG G 12
        Goto Spawn
        
      Melee:
        RANG G 0 A_JumpIfInventory("UsingLG", 1, "Melee2")
        RANG G 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound2")
        goto Melee2
        
      QuadSound2:
        RANG G 0 A_PlaySound("quakeweps/quadfire", 7)
        goto Melee2
        
      Melee2:
        RANG Z 2 bright
        RANG H 4 bright
        RANG G 12
        Goto Spawn
        
      Pain:
        RANG F 4
        RANG F 4 A_Pain
        Goto Spawn
        
      Pain.WolfenFlamethrower:
      Pain.MarathonPlasma:
      Pain.Slime:
      Pain.HexGreenFire:
      Pain.QuakeFire:
      Pain.FireExplosive:
      Pain.Fire:
        RANG F 4
        RANG F 4 A_PlaySound("ranger/firepain", CHAN_VOICE)
        goto Spawn
        
      Pain.Drowning:
        RANG F 4
        RANG F 4 A_PlaySound("ranger/drowning", CHAN_VOICE)
        goto Spawn
        
      Pain.Falling:
        RANG F 2
        RANG F 6 A_PlaySound("ranger/fallpain", CHAN_VOICE)
        goto Spawn
        
      Death:
        TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
      Death2:
        RANG GGGG 1 A_JumpIfHealthLower(-100, "XDeath")
        RANG I 5 A_PlayerScream
        RANG J 5 A_NoBlocking
        RANG KLM 4
        RANG N -1
        Stop
        
      Death.Drowning:
        RANG GGGG 1 A_JumpIfHealthLower(-100, "XDeath")
        RANG I 5 A_PlaySound("ranger/drown", CHAN_VOICE)
        RANG J 5 A_NoBlocking
        RANG KLM 4
        RANG N -1
        Stop
        
      Death.Telefrag:
        goto XDeath.Telefrag
        
      XDeath:
        TNT1 A 0 A_PlaySound("ranger/gib", CHAN_VOICE)
        goto Explod
        
      XDeath.Telefrag:
        TNT1 A 0 A_PlaySound("ranger/telefragged", CHAN_VOICE)
        goto Explod
        
      Death.CorvusGrimBallista:
        TNT1 A 0 A_PlaySoundEx("*falling",CHAN_VOICE,0,0)
        goto Explod
        
      Explod:
        TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,10)
      XDeath2:
        ACLO EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_SpawnItemEx("Blood", 0, 0, random(8, 56), random(-4, 4), 0, random(0, 6))
        ACLO E 0 A_SpawnItemEx("QuakeSmallGibDropper",0,0,random(20,40),0,0,0,0,1)
        QGBA C 1 ThrustThing(random(0, 255), random(0, 4), 1, 0)
        QGBA C 0 A_ChangeFlag("HEXENBOUNCE", 1)
        QGBA C -1 ThrustThingZ(0, random(36, 60), 0, 1)
        Stop
        
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        RANG F 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        RANG F 3 A_SpawnItem("ZorchEffectSmall")
        RANG F 3 A_SetTranslucent(0.9,0)
        TNT1 A 0 A_PlayerScream
        RANG F 3 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        RANG F 3 A_SetTranslucent(0.7,0)
        RANG F 3 A_SetTranslucent(0.6,0)
        RANG F 3 A_SetTranslucent(0.5,0)
        RANG F 3 A_SetTranslucent(0.4,0)
        RANG F 3 A_SetTranslucent(0.3,0)
        RANG F 3 A_SetTranslucent(0.2,0)
        RANG F 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop
        
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Death
        
      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-5,"DukeGibStart")
        goto Death.DukeDamage
        
      DukeGibStart:
        RANG F 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto XDeath
        
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Ice
        
      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_PlayWeaponSound("misc/freeze")
        RANG X 70 A_NoBlocking//A_GenericFreezeDeath
        RANG X 0 A_SetTranslucent(0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(8,48),random(-2,2),random(-2,2),random(1,6))
        RANG X 1 A_PlayWeaponSound("misc/icebreak")//A_FreezeDeathChunks
        RANG X -1
        stop
        //RANG F 5 A_GenericFreezeDeath
        //RANG F 1 A_FreezeDeathChunks
        //Wait
        
      CoOpModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 A_ChangeFlag("DONTBLAST",1)
        RANG A 1
        goto Spawn
        
      Pain.MightyBoot:
        RANG F 1 ACS_ExecuteAlways(312, 0, 25, 5, 24)
        RANG F 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        RANG F 6 A_Pain
        Goto See
        
      Death.HexGreenFire:
      Death.QuakeFire:
      Death.FireExplosive:
      Burn:
        RBRN I 1 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(12,42),random(-2,2),random(-2,2),random(0,3),random(-180,180),32)
        RBRN I 1 A_PlaySound("*burndeath")
        RBRN IIII 1 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(11,37),random(-2,2),random(-2,2),random(0,3),random(-180,180),32)
        RBRN J 1 A_NoBlocking
        RBRN JJJJ 1 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(10,32),random(-2,2),random(-2,2),random(0,3),random(-180,180),32)
        RBRN KKKKLLLLMMMM 1 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(8,22),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN NNNNNNNNNNNN 2 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(5,12),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN NNNNNNNNNNNN 3 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(5,12),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN NNNNNNNNNNNN 4 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(5,12),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN NNNNNNNNNNNN 5 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(5,12),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN NNNNNNNNNNNN 6 A_SpawnItemEx("QuakeBurn",random(-10,10),random(-10,10),random(5,12),random(-1,1),random(-1,1),random(0,2),random(-180,180),32)
        RBRN N -1
        Stop
        
      Pain.MarathonStun:
        TNT1 A 0 A_Stop
        RANG F 1 ACS_ExecuteAlways(312, 0, 25, 5, 100)
        TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_SetBlend("White",0.9,105)
        RANG F 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        RANG F 6 A_Pain
        Goto See
        
      XDeathNashgore:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("NashGore_Gib9",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33)
        TNT1 A 0 A_SpawnItemEx("NashGore_Gib10",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33)
        TNT1 AA 0 A_SpawnItemEx("NashGore_Gib11",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33)
        TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        goto XDeath2
        
      DeathBrutal:
        TNT1 A 0
        TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160)
        goto Death2
        
      XDeathBrutal:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy1",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy2",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy3",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalBrownOrgan",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalRedOrgan",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 AAAA 0 A_SpawnItemEx("BrutalChunk",random(-10,10),random(-10,10),random(12,40),random(-500,500)/100.00,random(-500,500)/100.00,random(300,1000)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalChunkHuge",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalSkull",0,0,56,random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 AAAAAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAAA 0 A_SpawnItemEx("BrutalTinyBouncyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("RedMist",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
        TNT1 AAAAA 0 A_SpawnItemEx("RedMistIntense",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound",0,0,0,0,0,0,0,160)
        goto XDeath2
        
      DeathHappyfun:
        TNT1 A 0
        TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto Death2
        
      XDeathHappyfun:
        TNT1 A 0
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        TNT1 AAAA 0 A_SpawnItemEx("FancyWhiteSmoke",random(-10,10),random(-10,10),random(-10,-10),random(-200,200)/100.00,random(-200,200)/100.00,random(200,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("HappyOnlineSound",0,0,0,0,0,0,0,160)
        goto XDeath2
    }
}

Actor RangerHead
{
    Projectile
    Damage 0
    +DOOMBOUNCE
    Speed 7
    Scale 0.34
    BounceFactor 0.4
    -NOLIFTDROP
    -SOLID
    +DROPOFF
    -NOGRAVITY
    -NOBLOCKMAP
    States
    {
      Spawn:
        QGBA C 4 A_Gravity
        QGBA C 0 A_CustomMissile("Blood",0,0,0,0)
        Loop
        
      Death:
        TNT1 A 0 A_Gravity
        QGBA C 1000
        Stop
    }
}

Actor QuakeSmallGibDropper
{
    Speed 0
    Projectile
    RenderStyle None
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 AAA 0 A_Jump(180,"Drop2","Drop3","Drop4")
        TNT1 AAA 0 A_SpawnItemEx("SmallGib1",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 A 0 A_SpawnItemEx("SmallGib2",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 A 0 A_SpawnItemEx("SmallGibTorso",0,0,0,random(-8,8),0,0,random(1,359),1)
        //TNT1 A 0 A_SpawnItemEx("RangerHead",0,0,20,random(1,7),0,random(1,7),random(0,359),32)
        TNT1 A 1
        Stop
        
      Drop2:
        TNT1 AAA 0 A_SpawnItemEx("SmallGib1",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 AA 0 A_SpawnItemEx("SmallGib2",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 A 0 A_SpawnItemEx("SmallGibTorso",0,0,0,random(-8,8),0,0,random(1,359),1)
        //TNT1 A 0 A_SpawnItemEx("RangerHead",0,0,20,random(1,7),0,random(1,7),random(0,359),32)
        TNT1 A 1
        Stop
        
      Drop3:
        TNT1 AA 0 A_SpawnItemEx("SmallGib1",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 AAA 0 A_SpawnItemEx("SmallGib2",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 A 0 A_SpawnItemEx("SmallGibTorso",0,0,0,random(-8,8),0,0,random(1,359),1)
        //TNT1 A 0 A_SpawnItemEx("RangerHead",0,0,20,random(1,7),0,random(1,7),random(0,359),32)
        TNT1 A 1
        Stop
        
      Drop4:
        TNT1 AA 0 A_SpawnItemEx("SmallGib1",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 AA 0 A_SpawnItemEx("SmallGib2",0,0,0,random(-8,8),0,0,random(1,359),1)
        TNT1 A 0 A_SpawnItemEx("SmallGibTorso",0,0,0,random(-8,8),0,0,random(1,359),1)
        //TNT1 A 0 A_SpawnItemEx("RangerHead",0,0,20,random(1,7),0,random(1,7),random(0,359),32)
        TNT1 A 1
        Stop
    }
}

actor SmallGib1
{
    BounceFactor 0.45
    +DOOMBOUNCE
    Speed 8
    Projectile
    -NOGRAVITY
    -NOBLOCKMAP
    -NOLIFTDROP
    -SOLID
    +DROPOFF
    +THRUACTORS
    Speed 8
    Scale 0.27
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ThrustThingZ(0,random(10,60),0,1)
        QGBB BBCCDDEEFFGGHHIIJJKKLLMMNN 2 A_SpawnItemEx("Blood",0,0,0,0,0,0,0,1,128)
        Goto Death
        
      Death:
        TNT1 A 0 A_Gravity
        QGBB N 1000
        stop
    }
}

actor SmallGibTorso : SmallGib1 
{
    Speed 2
    Scale 0.23
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ThrustThingZ(0,random(10,50),0,1)
        QGBC AABBCCDDEEFF 2 A_SpawnItemEx("Blood",0,0,0,0,0,0,0,1,128)
        Goto Death
        
      Death:
        TNT1 A 0 A_Gravity
        QGBC F 1000
        Wait
    }
}

actor SmallGib2 : SmallGib1 
{
    Speed 5
    Scale 0.19
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ThrustThingZ(0,random(10,60),0,1)
        QGBD AABBCCDDEE 2 A_SpawnItemEx("Blood",0,0,0,0,0,0,0,1)
        Goto Death
        
      Death:
        TNT1 A 0 A_Gravity
        QGBD F 1000
        Wait
    }
}

actor QuakeClass: Boolean {}

actor QuakeBullet: FastProjectile
{
    Damage (4)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    //Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop
        
      Death:
        TNT1 A 1
        TNT1 A 1 A_SpawnItem("QuakeBulletPuff")
        stop
        
      Crash:
      XDeath:
        TNT1 A 1
        stop
    }
}

actor QuakeBulletSpectral: QuakeBullet
{
    -THRUGHOST
    DamageType "Spectral"
}

actor QuakeBullet2: QuakeBullet
{
    -THRUGHOST
    DamageType "QuakePistol"
}

actor TelefragTester: Rocket
{
    DamageType "Telefrag"
}

actor QuakeInvisibility: CustomInventory
{
    +AUTOACTIVATE
    +ALWAYSPICKUP
    Inventory.MaxAmount 0
    
    States
    {
      Pickup:
        TNT1 A 0 A_SetTranslucent(0.15, 0)
        TNT1 A 0 A_ChangeFlag("SHADOW", 1)
        stop
    }
}

actor QuakeInvisibilityOff: CustomInventory
{
    +AUTOACTIVATE
    +ALWAYSPICKUP
    Inventory.MaxAmount 0
    
    States
    {
      Pickup:
        TNT1 A 0 A_SetTranslucent(1.0, 0)
        TNT1 A 0 A_ChangeFlag("SHADOW", 0)
        stop
    }
}

actor QuakeBurn
{
    Alpha 0.5
    Scale 0.25
    
    RenderStyle Add
    
    +CLIENTSIDEONLY
    +NOINTERACTION
    
    States
    {
      Spawn:
        CFFX CDEFGHIJ 2 bright A_FadeOut(0.025)
        loop
    }
}
