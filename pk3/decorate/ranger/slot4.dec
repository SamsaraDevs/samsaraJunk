actor "Nailgun": InstantSwitchWeapon
{
    Weapon.BobRangeX 0
    Weapon.BobRangeY 0.5
    Weapon.BobSpeed 1.1
    Weapon.BobStyle Alpha
    Weapon.Kickback 100
    Weapon.SelectionOrder 5

    Weapon.AmmoUse 1
    Weapon.AmmoGive 30
    Weapon.AmmoType "Clip"

    Weapon.AmmoGive2 0
    Weapon.AmmoType2 "LavaNails"

    Weapon.SisterWeapon "Spectral NG"

    +UNDROPPABLE
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +NOALERT

    Inventory.Icon "QNLGZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the nailgun"
    Obituary "%k \csnailed\c- %o."

    States
    {
      Deselect:
        QRKL A 0 A_TakeInventory("FiredLavaNails")
        QRKL A 0 A_TakeInventory("UsingDoEAmmo")
        goto Super::Deselect

      Ready:
        TNT1 A 0 A_JumpIfInventory("FiredLavaNails",1,"LavaReady")
        QNLG A 1 A_WeaponReady
        TNT1 A 0 A_TakeInventory("FiredLavaNails",1)
        TNT1 A 0 A_TakeInventory("UsingDoEAmmo",1)
        loop

      LavaReady:
        QLNG A 1 A_WeaponReady
        TNT1 A 0 A_GiveInventory("FiredLavaNails",1)
        TNT1 A 0 A_GiveInventory("UsingDoEAmmo",1)
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("FiredLavaNails",1,"LavaFire")
		TNT1 A 0 A_JumpIfNoAmmo("DeselectShit")
        TNT1 A 0 A_TakeInventory("FiredLavaNails")
        TNT1 A 0 A_TakeInventory("UsingDoEAmmo")
        QNLG A 0 A_GunFlash
        QNLG A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
        goto FireNail

      FireNail:
        QNLG A 0 A_JumpIfInventory("NailgunSide", 1, "NailRight")
        goto NailLeft

      LavaFire:
		TNT1 A 0 A_JumpIfInventory("LavaNails",1,"LavaFire2")
		goto AltFire2

      LavaFire2:
        TNT1 A 0 A_GiveInventory("FiredLavaNails")
        TNT1 A 0 A_GiveInventory("UsingDoEAmmo")
        QNLG A 0 A_GunFlash
        QSNL A 0 A_TakeInventory("LavaNails", 1)
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters
        QNLG A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
        goto FireAlt

      FireAlt:
        QNLG A 0 A_JumpIfInventory("NailgunSide", 1, "AltRight")
        goto AltLeft
        
      AltFire:
        TNT1 A 0 A_JumpIfInventory("FiredLavaNails",1,"AltFire2")
        TNT1 A 0 A_PlaySound("quakeweps/doeammochange")
        TNT1 A 0 A_GiveInventory("FiredLavaNails",1)
        TNT1 A 0 A_GiveInventory("UsingDoEAmmo",1)
        QLNG A 5 A_WeaponReady(12)
        TNT1 A 0 A_GiveInventory("FiredLavaNails",1)
        QLNG A 10 A_WeaponReady(8)
        goto LavaReady
        
      AltFire2:
        TNT1 A 0 A_PlaySound("quakeweps/doeammochange")
        TNT1 A 0 A_TakeInventory("FiredLavaNails",1)
        TNT1 A 0 A_TakeInventory("UsingDoEAmmo",1)
        QNLG A 5 A_WeaponReady(12)
        TNT1 A 0 A_TakeInventory("FiredLavaNails",1)
        QNLG A 10 A_WeaponReady(8)
        goto Ready

      NailLeft:
        QNLG A 0 A_FireCustomMissile("QNTrail1",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QNTrail2",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QNTrail3",  0, 0, -3)
        QNLG A 0 A_JumpIfInventory("CoopModeOn", 1, "NailLeftCoop")
        QNLG A 0 A_JumpIfInventory("SPModeOn", 1, "NailLeftCoop")
        QNLG A 0 A_FireCustomMissile("QuakeNail", 0, 1, -3)
        goto NailLeft2

      NailLeftCoop:
        QNLG A 0 A_FireCustomMissile("QuakeNailCoop", 0, 1, -3)
        goto NailLeft2

      NailLeft2:
        QNLG A 0 A_GiveInventory("NailgunSide")
        QNLG A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageNailLeft")
        QNLG A 0 ACS_ExecuteAlways(312, 0, -33, 1, 2)
        QNLG D 1 bright 
        QNLG E 1 bright 
        QNLG A 1 bright 
        QNLG C 0 A_Refire
        goto Ready

      RageNailLeft:
        QNLG A 0 ACS_ExecuteAlways(312, 0, -33, 0, 1)
        QNLG D 2 bright 
        GNLG D 0 
        QNLG C 0 A_Refire
        goto Ready

      NailRight:
        QNLG A 0 A_FireCustomMissile("QNTrail1",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QNTrail2",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QNTrail3",  0, 0, 3)
        QNLG A 0 A_JumpIfInventory("CoopModeOn", 1, "NailRightCoop")
        QNLG A 0 A_JumpIfInventory("SPModeOn", 1, "NailRightCoop")
        QNLG A 0 A_FireCustomMissile("QuakeNail", 0, 1, 3)
        goto NailRight2

      NailRightCoop:
        QNLG A 0 A_FireCustomMissile("QuakeNailCoop", 0, 1, 3)
        goto NailRight2

      NailRight2:
        QNLG A 0 A_TakeInventory("NailgunSide")
        QNLG A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageNailRight")
        QNLG A 0 ACS_ExecuteAlways(312, 0, -33, 1, 3)
        QNLG B 1 bright 
        QNLG C 1 bright 
        QNLG A 2 bright 
        QNLG B 0 A_Refire
        goto Ready

      RageNailRight:
        QNLG A 0 ACS_ExecuteAlways(312, 0, -73, 0, 2)
        QNLG B 2 bright 
        QNLG A 2 bright 
        QNLG C 0 A_Refire
        goto Ready

      AltLeft:
        QNLG A 0 A_FireCustomMissile("QLTrail1",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QLTrail2",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QLTrail3",  0, 0, -3)
        QNLG A 0 A_JumpIfInventory("CoopModeOn", 1, "AltLeftCoop")
        QNLG A 0 A_JumpIfInventory("SPModeOn", 1, "AltLeftCoop")
        QNLG A 0 A_FireCustomMissile("QuakeLavaNail", 0, 0, -3)
        goto LavaLeft

      AltLeftCoop:
        QNLG A 0 A_FireCustomMissile("QuakeLavaNailCoop", 0, 0, -3)
        goto LavaLeft


      AltRight:
        QNLG A 0 A_FireCustomMissile("QLTrail1",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QLTrail2",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QLTrail3",  0, 0, 3)
        QNLG A 0 A_JumpIfInventory("CoopModeOn", 1, "AltRightCoop")
        QNLG A 0 A_JumpIfInventory("SPModeOn", 1, "AltRightCoop")
        QNLG A 0 A_FireCustomMissile("QuakeLavaNail", 0, 0, 3)
        goto LavaRight

      AltRightCoop:
        QNLG A 0 A_FireCustomMissile("QuakeLavaNailCoop", 0, 0, 3)
        goto LavaRight

      LavaLeft:
        QLNG A 0 A_GiveInventory("NailgunSide")
        QLNG A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageLavaLeft")
        QNLG A 0 ACS_ExecuteAlways(312, 0, -50, 1, 3)
        QLNG D 1 bright 
        QLNG E 1 bright 
        QLNG A 1 bright 
        QLNG C 0 A_Refire
        QLNG A 0 A_PlaySound("quakeweps/lavaend", 5)
        goto LavaReady

      RageLavaLeft:
        QNLG A 0 ACS_ExecuteAlways(312, 0, -50, 0, 1)
        QLNG D 2 bright 
        GLNG D 0 
        QLNG C 0 A_Refire
        QLNG A 0 A_PlaySound("quakeweps/lavaend", 5)
        goto LavaReady

      LavaRight:
        QLNG A 0 A_TakeInventory("NailgunSide")
        QLNG A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageLavaRight")
        QNLG A 0 ACS_ExecuteAlways(312, 0, -50, 1, 4)
        QLNG B 1 bright 
        QLNG C 1 bright 
        QLNG A 2 bright 
        QLNG B 0 A_Refire
        QLNG A 0 A_PlaySound("quakeweps/lavaend", 5)
        goto LavaReady

      RageLavaRight:
        QNLG A 0 ACS_ExecuteAlways(312, 0, 1, 0, 2)
        QLNG B 2 bright 
        QLNG A 2 bright 
        QLNG C 0 A_Refire
        QLNG A 0 A_PlaySound("quakeweps/lavaend", 5)
        goto LavaReady

      Flash:
        TNT1 A 2 A_Light2
        Goto LightDone

      Spawn:
        QNLG Z -1
        Stop

      DeselectShit:
        QNLG A 1
        TNT1 A 0 A_JumpIfInventory("Thunderbolt",1,"ConsiderTB")
      Consider6:
        TNT1 A 0 A_JumpIfInventory("GotWeapon5",1,"ConsiderQRL")
      Consider5:
        TNT1 A 0 A_JumpIfInventory("GotWeapon3",1,"ConsiderQGL")
      Consider4:
        TNT1 A 0 A_JumpIfInventory("GotWeapon6",1,"ConsiderSNG")
      Consider3:
        //TNT1 A 0 A_JumpIfInventory("GotWeapon4",1,"ConsiderNG")
      Consider2:
        TNT1 A 0 A_JumpIfInventory("GotWeapon2",1,"ConsiderDSG")
      Consider1:
        TNT1 A 0 A_JumpIfInventory("GotWeapon0",1,"ConsiderLasCan")
      Consider0:
        QNLG A 1 A_SelectWeapon("Single Shotgun")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
		goto Deselect
		
	  ConsiderTB:
		TNT1 A 0 A_JumpIfInventory("Cell",1,"SelectTB")
		goto Consider6
	  ConsiderQRL:
		TNT1 A 0 A_JumpIfInventory("Shell",1,"SelectQRL")
		goto Consider5
	  ConsiderQGL:
		TNT1 A 0 A_JumpIfInventory("Shell",1,"SelectQGL")
		goto Consider4
	  ConsiderSNG:
		TNT1 A 0 A_JumpIfInventory("Clip",1,"SelectSNG")
		goto Consider3
	  ConsiderNG:
		TNT1 A 0 A_JumpIfInventory("Clip",1,"SelectNG")
		goto Consider2
	  ConsiderDSG:
		TNT1 A 0 A_JumpIfInventory("Shell",1,"SelectDSG")
		goto Consider1
	  ConsiderLasCan:
		TNT1 A 0 A_JumpIfInventory("Cell",1,"SelectLasCan")
		goto Consider0

	  SelectTB:
		TNT1 A 0 A_SelectWeapon("Thunderbolt")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
      SelectQRL:
        TNT1 A 0 A_SelectWeapon("  Rocket Launcher  ")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
      SelectQGL:
        TNT1 A 0 A_SelectWeapon("Grenade Launcher")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
      SelectSNG:
        TNT1 A 0 A_SelectWeapon("Super Nailgun")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
      /*SelectNG:
        TNT1 A 0 A_SelectWeapon("Nailgun")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect*/
      SelectDSG:
        TNT1 A 0 A_SelectWeapon("Double Shotgun")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
      SelectLasCan:
        TNT1 A 0 A_SelectWeapon("Laser Cannon")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    }
}

actor "Spectral NG": "Nailgun"
{
    Weapon.AmmoType ""
    Weapon.AmmoUse  0
    Weapon.AmmoGive 0

    Weapon.AmmoType2 ""
    Weapon.AmmoUse2  0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon "Nailgun"
    +POWERED_UP

    States
    {
      Fire:
        TNT1 A 0 A_JumpIfInventory("FiredLavaNails", 1, "MultiFire")

        TNT1 A 0 A_TakeInventory("FiredLavaNails")
        TNT1 A 0 A_TakeInventory("UsingDoEAmmo")

        TNT1 A 0 ACS_ExecuteAlways(317, 0, 2,0,0)
        QNLG A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters
        QNLG A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("weapons/sigil", 6)
        QNLG A 0 A_JumpIfInventory("NailgunSide", 1, "NailRight")
        goto NailLeft

      NailLeft:
        QNLG A 0 A_FireCustomMissile("QNTrail1",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QNTrail2",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QNTrail3",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("SpectralNail", 0, 1, -3)
        goto NailLeft2

      NailRight:
        QNLG A 0 A_FireCustomMissile("QNTrail1",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QNTrail2",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QNTrail3",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("SpectralNail", 0, 1, 3)
        goto NailRight2


      MultiFire:
        TNT1 A 0 A_GiveInventory("FiredLavaNails")
        TNT1 A 0 A_GiveInventory("UsingDoEAmmo")
        TNT1 A 0 ACS_ExecuteAlways(317, 0, 4,0,0)
        QNLG A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters
        QNLG A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("weapons/sigil", 6)
        QNLG A 0 A_JumpIfInventory("NailgunSide", 1, "AltRight")
        goto AltLeft

      AltLeft:
        QNLG A 0 A_FireCustomMissile("QLTrail1",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QLTrail2",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("QLTrail3",  0, 0, -3)
        QNLG A 0 A_FireCustomMissile("SpectralLavaNail", 0, 1, -3)
        goto LavaLeft

      AltRight:
        QNLG A 0 A_FireCustomMissile("QLTrail1",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QLTrail2",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("QLTrail3",  0, 0, 3)
        QNLG A 0 A_FireCustomMissile("SpectralLavaNail", 0, 1, 3)
        goto LavaRight
    }
}

actor QNTrail1
{
    Speed 54

    Projectile
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 1, 38, 88)
        goto SpawnLoop

      SpawnLoop:
        TNT1 A 1 A_SpawnItemEx("QuakeNailTrail", 0,0,0, random(-3,3)/10.0,random(-3,3)/10.0,random(-3, 3)/10.0)
        loop

    }
}

actor QNTrail2: QNTrail1
{
    Speed 37

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 2, 02, 7)
        goto SpawnLoop
    }
}

actor QNTrail3: QNTrail1
{
    Speed 18

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 4, 16, 66)
        goto SpawnLoop
    }
}

actor QuakeNail: FastProjectile
{
    Radius 1
    Height 0
    Speed 75

    Projectile
    Damage (9)

    +BLOODSPLATTER

    //Decal BulletChip
    DeathSound "quakeweps/nailricochet"
    
    States
    {
      Spawn:
        QKNL A 0
        goto SpawnLoop

      SpawnLoop:
        QKNL A 1 A_SpawnItemEx("QuakeNailTrail", 0,0,0, random(-3,3)/10.0,random(-3,3)/10.0,random(-3, 3)/10.0)
        loop

      XDeath:
        TNT1 A 0
        stop

      Death:
        TNT1 A 1
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff2",random(-5,5),random(-5,5),random(-5,-5),0,0,random(-10,1)/10)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("QuakeBulletPuff3",random(-5,5),random(-5,5),random(-5,5),0,0,random(-10,1)/10)
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff4",random(-5,5),random(-5,5),random(-5,5),0,0,random(-10,1)/10)
        stop
    }
}

actor QuakeNailCoop: QuakeNail
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (10)
}


actor QLTrail1
{
    Speed 54

    Projectile
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 1, 38, 88)
        goto SpawnLoop

      SpawnLoop:
        TNT1 A 1 A_SpawnItemEx("QuakeLavaTrail", 0,0,0, random(-3,3)/10.0,random(-3,3)/10.0,random(-3, 3)/10.0)
        loop

    }
}

actor QLTrail2: QLTrail1
{
    Speed 37

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 2, 02, 7)
        goto SpawnLoop
    }
}

actor QLTrail3: QLTrail1
{
    Speed 18

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteAlways(586, 0, 4, 16, 66)
        goto SpawnLoop
    }
}

actor QuakeLavaNail: QuakeNail
{
    Damage (14)
    +PIERCEARMOR
    DamageType Fire

    Translation "160:164=208:214"

    Obituary "%k \csthought\c- %o \cswas hot."
    
    States
    {
      Spawn:
        QKNL A 0
        goto SpawnLoop

      SpawnLoop:
        QKNL A 1 A_SpawnItemEx("QuakeLavaTrail", 0,0,0, random(-3,3)/10.0,random(-3,3)/10.0,random(-3, 3)/10.0)
        loop

      Death:
        TNT1 A 1 A_PlaySound("quakeweps/nailricochet")
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff2",random(-5,5),random(-5,5),random(-5,-5),0,0,random(-10,1)/10)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("QuakeBulletPuff3",random(-5,5),random(-5,5),random(-5,5),0,0,random(-10,1)/10)
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff4",random(-5,5),random(-5,5),random(-5,5),0,0,random(-10,1)/10)
        TNT1 A 0 A_SpawnItemEx("LavaNailBounce")
        stop
    }
}

actor QuakeLavaNailCoop: QuakeLavaNail
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (15)
}


actor NailBounce
{
    +CLIENTSIDEONLY
    +NOCLIP

    ReactionTime 24
    
    States
    {
      Spawn:
        TNT1 A 0 A_Countdown
        TNT1 A 1 A_SpawnItemEx("QuakeNailTrail2")
        loop
    }
}

actor LavaNailBounce
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.8
    Scale 0.5

    States
    {
      Spawn:
        CFCF ACEGIKNOP 2 bright
        stop
    }
}

actor QuakeNailTrail: DoomBulletPuff
{
    Alpha 0.1
    Scale 0.5

    +CLIENTSIDEONLY
    +NOINTERACTION
}

actor QuakeLavaTrail
{
    Alpha 0.3
    Scale 0.5

    RenderStyle Add

    +CLIENTSIDEONLY
    +NOINTERACTION

    States
    {
      Spawn:
        CFFX NOP 2 bright
        stop
    }
}

actor QuakeNailTrail2: BulletPuff
{
    Alpha 0.5
    Scale 0.5

    +CLIENTSIDEONLY
    +NOINTERACTION

    States
    {
      Spawn:
        DPUF A 2
        DPUF A 2 A_FadeOut(0.4)
        DPUF BCD 4
        stop
    }
}

actor NailgunSide: Inventory
{
    inventory.maxamount 2
    +UNDROPPABLE
}

actor NailgunFrame: Inventory
{
    inventory.maxamount 4
    +UNDROPPABLE
}

actor FiredLavaNails: Boolean {}

actor SpectralNail: QuakeNail { DamageType "Spectral" }
actor SpectralLavaNail: QuakeLavaNail { DamageType "Spectral" }
