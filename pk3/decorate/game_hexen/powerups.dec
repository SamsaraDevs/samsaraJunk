actor HexenTeamPickup : CustomInventory replaces ArtiHealingRadius
{
    +Floatbob
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +INVENTORY.HUBPOWER
    +COUNTITEM
    Inventory.PickupMessage "$SAMSARA_POWERUP_TEAM"
    Inventory.PickupSound "nosoundforyoubitch"
    states
    {
      Spawn:
        HRAD ABCDEFGHIJKLMNOP 4 Bright
        Loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupMarathon")
        TNT1 A 0 A_JumpIfInventory("DoomguyClass",1,"PickupDoom")
        TNT1 A 0 A_JumpIfInventory("ChexClass",1,"PickupChex")
        TNT1 A 0 A_JumpIfInventory("WolfenClass",1,"PickupWolfen")
        TNT1 A 0 A_JumpIfInventory("CorvusClass",1,"PickupHeretic")
        TNT1 A 0 A_JumpIfInventory("HexenClass",1,"PickupHexen")
        TNT1 A 0 A_JumpIfInventory("DukeClass",1,"PickupDuke")
        TNT1 A 0 A_JumpIfInventory("QuakeClass",1,"PickupQuake")
        TNT1 A 1
        fail

      PickupDoom:
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("HealPack0",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)//A_SpawnDebris("HealPack0")
        stop

      PickupChex:
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("ArmorScrap",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0) //A_SpawnDebris("ArmorScrap")
        stop

      PickupWolfen:
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 AAA 1 A_SpawnItemEx("Ammo1Small",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)//A_SpawnDebris("Ammo1Small")
        TNT1 AAA 1 A_SpawnItemEx("Ammo2Small",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)//A_SpawnDebris("Ammo1Small")
        stop

      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("ArtiHealingRadius2",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("ArtiHealingRadius2")
        stop

      PickupHexen:
        TNT1 A 0 A_JumpIfInventory("PortMysticAmbit",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("PortMysticAmbit")
        stop

      PickupHereticFail:
        HRAD A 4 BRIGHT
        fail

      PickupDuke:
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 AAAAA 1 A_SpawnItemEx("Ammo3Small",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)//A_SpawnDebris("Ammo1Small")
        stop

      PickupMarathon:
        TNT1 A 0 A_SetBlend("White",0.5,5)
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 A 1 A_GiveInventory("Allmap",1)
        TNT1 A 1 A_GiveInventory("PowerScanner",1)
        stop

      PickupQuake:
        TNT1 A 0 A_PlaySound("MysticIncant")
        TNT1 AAAA 1 A_SpawnItemEx("QuakeQuarterDamage",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)
        stop
    }
}

actor ArtiHealingRadius2: ArtiHealingRadius {
Inventory.MaxAmount 16
//Tag "Mystic Ambit Incant"
}

Actor QuakeQuarterDamage : PowerupGiver
{
  +COUNTITEM
  +FLOATBOB
  -INVBAR
  +INVENTORY.PICKUPFLASH
  +INVENTORY.AUTOACTIVATE
  Powerup.Type QuackQuarterDamage
  RenderStyle Add
  Alpha 0.9
  Scale 0.2
  Inventory.PickupAnnouncerEntry "strength"
  Inventory.PickupSound "quakeweps/quadready"
  Inventory.PickupMessage "$SAMSARA_POWERUP_QUARTERDMG"
  States
  {
  Spawn:
    QRTD A -1
    stop
  }
}

Actor PowerQuackQuarterDamage : PowerDamage
{
  Powerup.Color "00 50 FF", 0.1
  Powerup.Duration -60
  DamageFactor "Pistol", 1.25
  DamageFactor "DoomFist", 1.25
  DamageFactor "ZorchPistol", 1.25
  DamageFactor "Wraithverge", 1.25
  DamageFactor "Mace", 1.25
  DamageFactor "DragonClaw", 1.25
  DamageFactor "Normal", 1.25
  DamageFactor "Zorch", 1.25
  DamageFactor "PhasingZorch", 1.25
  DamageFactor "BFG", 1.25
  DamageFactor "BFGSpray", 1.25
  DamageFactor "Fire", 1.25
  DamageFactor "Ice", 1.25
  DamageFactor "Disintegrate", 1.25
  DamageFactor "WolfenFlamethrower", 1.25
  DamageFactor "DukeDamage", 1.25
  DamageFactor "DukeExplosive", 1.25
  DamageFactor "DukeIce", 1.25
  DamageFactor "MightyBoot", 1.25
  
  DamageFactor "DukePistol", 1.25
	DamageFactor "MarathonPistol", 1.25
	DamageFactor "Explosive", 1.25
	DamageFactor "MarathonFusion", 1.25
	DamageFactor "DoomPlasma", 1.25
	DamageFactor "CorvusCrossbow", 1.25
	DamageFactor "CorvusHellstaff", 1.25
	DamageFactor "CorvusHellstaffTomed", 1.25
	DamageFactor "MarathonPlasma", 1.25
	DamageFactor "BJSpear", 1.25
	DamageFactor "WMCBeam", 1.25
	DamageFactor "WMCNade", 1.25
	DamageFactor "QuakePistol", 1.25
	DamageFactor "NotQuadded", 1.25
}

actor HeXenBeaconItem: CustomInventory replaces ArtiDarkServant
{
    Radius 16
    Height 16
	Scale 0.3
    Inventory.MaxAmount 5
    Inventory.PickupMessage "$SAMSARA_BEACON_HEXEN"

    Inventory.Icon "ARTICPRT"
    +INVBAR
	+FLOATBOB

    States
    {
      Spawn:
        CPRT AAAAABCDEFEDCB 5
        loop

      Use:
        TNT1 A 0 A_JumpIfInventory("DoomguyClass",  1, "UseDoom")
        TNT1 A 0 A_JumpIfInventory("ChexClass",     1, "UseChex")
        TNT1 A 0 A_JumpIfInventory("CorvusClass",   1, "UseHeretic")
        TNT1 A 0 A_JumpIfInventory("WolfenClass",   1, "UseWolf")
        TNT1 A 0 A_JumpIfInventory("HexenClass",    1, "UseHexen")
        TNT1 A 0 A_JumpIfInventory("DukeClass",     1, "UseDuke")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "UseMarathon")
        TNT1 A 0 A_JumpIfInventory("QuakeClass",    1, "UseQuake")
        TNT1 A 0 A_FireCustomMissile("HexenBeaconSpawner", 0, 0)
        stop

      UseDoom:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconDoom", 0, 0)
        stop

      UseChex:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconChex", 0, 0)
        stop

      UseHeretic:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconHeretic", 0, 0)
        stop

      UseWolf:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconWolf", 0, 0)
        stop

      UseHexen:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconHexen", 0, 0)
        stop

      UseDuke:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconDuke", 0, 0)
        stop

      UseMarathon:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconMarathon", 0, 0)
        stop

      UseQuake:
        TNT1 A 0 A_FireCustomMissile("HexenBeaconQuake", 0, 0)
        stop
    }
}

actor HexenBeaconSpawner
{
    Radius 4
    Height 4

    Speed 8

    -SOLID

    ReactionTime 5

    States
    {
      Spawn:
        ACLO A 0
        ACLO E 1 A_JumpIf(1 == 1, "SpawnLoop")
        loop

      SpawnLoop:
        "----" A 35
        "----" A 160 ACS_ExecuteAlways(315, 0, 1)
        "----" AAAA 160 ACS_ExecuteAlways(315)
        stop

      WhatAmIDoingWithMyLife:
        "----" A 0 A_PlaySound("FighterHammerExplode")
        "----" A 0 A_SpawnItemEx("BangHeXenCloud", 0,0,16)
        "----" A 0 A_Explode(64, 160) // okay this is just silly
        stop

      FuckYouImAPickup:
        "----" A 0 A_PlaySound("FighterHammerExplode")
        "----" A 0 A_SpawnItemEx("BangHeXenCloud")
        "----" A 0 A_SpawnItemEx("HeXenBeaconItem", 0,0,0, random(0,4),0,random(0,8), random(0, 360))
        stop
    }
}

ACTOR BangHeXenCloud
{
  +NOBLOCKMAP
  +NOGRAVITY
  +CLIENTSIDEONLY
  RenderStyle Translucent
  VSpeed 1
  States
  {
  Spawn:
    FHFX IJKLMNOPQR 3 Bright
    Stop
  }
}

actor HexenBeaconDoom: HexenBeaconSpawner
{
    States
    {
      SpawnLoop:
        BEAC BC 12
        BEAC D  11
      // +35 (0)
        BEAC B 12 ACS_ExecuteAlways(315, 0, 1)
        BEAC CDBCDBCDBCDB 12
        BEAC C 4
      // +160 (1)
        BEAC C 8 ACS_ExecuteAlways(315)
        BEAC DBCDBCDBCDBC 12
        BEAC D 8
      // +160 (2)
        BEAC D 4 ACS_ExecuteAlways(315)
        BEAC BCDBCDBCDBCDB 12
      // +160 (3)
        BEAC C 12 ACS_ExecuteAlways(315)
        BEAC DBCDBCDBCDBC 12
        BEAC D 4
      // +160 (4)
        BEAC D 8 ACS_ExecuteAlways(315)
        BEAC BCDBCDBCDBCD 12
        BEAC B 8
        stop
    }
}

actor HexenBeaconChex: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC E 0
        goto SpawnLoop
    }
}

actor HexenBeaconHeretic: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC F 0
        goto SpawnLoop
    }
}

actor HexenBeaconWolf: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC G 0
        goto SpawnLoop
    }
}

actor HexenBeaconHexen: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC H 0
        goto SpawnLoop
    }
}

actor HexenBeaconDuke: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC I 0
        goto SpawnLoop
    }
}

actor HexenBeaconMarathon: HexenBeaconSpawner
{
    States
    {
      Spawn:
        BEAC J 0
        goto SpawnLoop
    }
}

actor HexenBeaconQuake: HexenBeaconSpawner // A wee bit wonky. Doesn't explode if there's no room anymore.
{
    States
    {
      Spawn:
      SpawnLoop:
        BEAC KK 12
        BEAC K  11
      // +35 (0)
        BEAC K 12 A_SpawnItemEx("FriendlyGruntHeXen",0,0,0,0,0,0,0,48)//ACS_ExecuteAlways(315, 0, 1)
        BEAC KKKKKKKKKKKK 12
        BEAC K 4
      // +160 (1)
        BEAC K 8 A_SpawnItemEx("FriendlyGruntHeXen",0,0,0,0,0,0,0,48)//ACS_ExecuteAlways(315)
        BEAC KKKKKKKKKKKK 12
        BEAC K 8
      // +160 (2)
        BEAC K 4 A_SpawnItemEx("FriendlyGruntHeXen",0,0,0,0,0,0,0,48)//ACS_ExecuteAlways(315)
        BEAC KKKKKKKKKKKKK 12
      // +160 (3)
        BEAC K 12 A_SpawnItemEx("FriendlyGruntHeXen",0,0,0,0,0,0,0,48)//ACS_ExecuteAlways(315)
        BEAC KKKKKKKKKKKK 12
        BEAC K 4
      // +160 (4)
        BEAC K 8 A_SpawnItemEx("FriendlyGruntHeXen",0,0,0,0,0,0,0,48)//ACS_ExecuteAlways(315)
        BEAC KKKKKKKKKKKK 12
        BEAC K 8
        stop
        BEAC K 0
        goto SpawnLoop
    }
}

actor HexenInvulnerability : InvulnPowerup replaces ArtiInvulnerability2 { }

actor HexenAmmoBackpack : AmmoBackpack replaces ArtiBoostMana { }

actor HexenBootsPickup : CustomInventory replaces ArtiSpeedBoots
{
    +Floatbob
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +INVENTORY.HUBPOWER
    +COUNTITEM
    Inventory.PickupAnnouncerEntry "Haste"
    Inventory.PickupMessage "$SAMSARA_POWERUP_SPEED"
    Inventory.PickupSound "nosoundforyoubitch"
    states
    {
      Spawn:
        SPED ABCDEFGH 3 Bright
        Loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupMarathon")
        TNT1 A 1 A_JumpIfInventory("HexenClass",1,"PickupHexen")
        TNT1 A 1 A_JumpIfInventory("CorvusClass",1,"PickupHeretic")
        TNT1 A 0 A_PlaySound("SorcererSpellCast")
        TNT1 A 1 A_GiveInventory("PowerSpeed")
        stop

      PickupMarathon:
        TNT1 A 0 A_SetBlend("Green",0.4,4)
        TNT1 A 0 A_PlaySound("SorcererSpellCast")
        TNT1 A 1 A_GiveInventory("PowerSpeed")
        stop

      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("ArtiSpeedBoots",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("ArtiSpeedBoots")
        stop

      PickupHexen:
        TNT1 A 0 A_JumpIfInventory("ArtiSpeedBoots",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("ArtiSpeedBoots")
        stop

      PickupHereticFail:
        SPED A 4
        fail
    }
}

actor HexenTeleportPickup : CustomInventory replaces ArtiTeleportOther
{
    +Floatbob
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +INVENTORY.HUBPOWER
    +COUNTITEM
    Inventory.PickupMessage "$SAMSARA_POWERUP_TELEPORTOTHER"
    Inventory.PickupSound "nosoundforyoubitch"
    states
    {
      Spawn:
        TELO ABCD 5 BRIGHT
        Loop

      Pickup:
        TNT1 A 1 A_JumpIfInventory("HexenClass",1,"PickupHexen")
        TNT1 A 0 A_JumpIfInventory("CorvusClass",1,"PickupHeretic")
        fail

      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("ArtiTeleportOther",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("ArtiTeleportOther")
        stop

      PickupHereticFail:
        TELO A 5 BRIGHT
        fail

      PickupHexen:
        TNT1 A 0 A_JumpIfInventory("ArtiTeleportOther",0,"PickupHereticFail")
        TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("ArtiTeleportOther")
        stop
    }
}