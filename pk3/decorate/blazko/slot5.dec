actor " Rocket Launcher ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "wolfen/rocketget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    //+AMMO_OPTIONAL
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Rocket Launcher!"
    Weapon.YAdjust 16
    States
    {
      Spawn:
        WRLP A -1
        Stop

      Ready:
        WRCK A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocket")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready

      CoopFire:
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocket2")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready
    }
}

actor WolfRocket
{
    Scale 0.75
    Radius 8
    Height 6
    Speed 30
    Damage 15
    Projectile
    +RIPPER
    +FORCERADIUSDMG
    +NOEXTREMEDEATH
    +NOBOSSRIP
    SeeSound "wolfen/missilefire"
    DeathSound "wolfen/missilexplod"
    Obituary "%k \cjtore through\c- %o \cjwith the Rocket Launcher.\c-"
    States
    {
      Spawn:
        WMSL A 3 A_SpawnItem("WolfRocketSmoke")
        Loop

      Death:
        WFLM C 8 Bright A_Explode(128,32,0)
        WFLM D 6 Bright
        WFLM E 4 Bright
        Stop
    }
}

actor WolfRocket2: WolfRocket { +THRUSPECIES Species "Player" }

actor WolfRocketSmoke
{
    Radius 1
    Height 1
    Speed 0
    Renderstyle Translucent
    Alpha 0.5
    +NOGRAVITY
    -SOLID
    +NOCLIP
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 2
        RTRL ABCD 4
        stop
    }
}
