actor "Mini-Zorcher" : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickUpSound "chex/weaponget"
    Obituary "%k \chzorched\c- %o \chwith his pea-blaster.\c-"
    Inventory.PickupMessage "You got the Mini-Zorcher!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +INVENTORY.UNDROPPABLE
    DamageType "ZorchPistol"
    States
    {
      Ready:
        PISC A 1 A_WeaponReady
        Loop

      Deselect:
        PISC A 1 A_Lower
        Loop

      Select:
        PISC A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        PISC A 4
        PISC B 0 A_FireBullets(5, 0, 1, 5, "ChexPuff2")
        PISC B 0 A_PlayWeaponSound("chex/pistol")
        PISC B 6 A_GunFlash
        PISC C 4
        PISC D 5 A_ReFire
        Goto Ready

      CoopFire:
        PISC A 4
        PISC B 0 A_FireCustomMissile("ChexBullet2",0,0,0,1)
        PISC B 0 A_PlayWeaponSound("chex/pistol")
        PISC B 6 A_GunFlash
        PISC C 4
        PISC D 5 A_ReFire
        Goto Ready

      Hold:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
        PISC A 4
        PISC B 0 A_FireBullets(5, 0, -1, 5, "ChexPuff2")
        PISC B 0 A_PlayWeaponSound("chex/pistol")
        PISC B 6 A_GunFlash
        PISC C 4
        TNT1 A 0 A_ReFire
        PISC D 5 A_ClearRefire
        Goto Ready

      CoopHold:
        PISC A 4
        PISC B 0 A_FireCustomMissile("ChexBullet2",random(-500,500)/100.00,0,0,1)
        PISC B 0 A_PlayWeaponSound("chex/pistol")
        PISC B 6 A_GunFlash
        PISC C 4
        TNT1 A 0 A_ReFire
        PISC D 5 A_ClearRefire
        Goto Ready

      Flash:
        CPIS A 7 Bright A_Light1
        Goto LightDone
        CPIS A 7 Bright A_Light0
        Goto LightDone

      Spawn:
        CIST A -1
        Stop
    }
}

actor ChexPuff2 : ChexPuff { -THRUGHOST DamageType "ZorchPistol" } // I never understood why you had to damagetype puffs on hitscan weapons.

actor ChexBullet2 : FastProjectile
{
    Damage (Random(1,3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "ZorchPistol"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("ChexPuff")
        stop
    }
}
