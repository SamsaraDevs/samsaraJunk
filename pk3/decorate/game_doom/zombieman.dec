actor Clip2: CustomInventory
{
    Inventory.PickupMessage "Picked up 5 rounds of Ammo 1."
    Inventory.PickupSound "ammo/bulletsmall"
    States
    {
      Spawn:
        CLIP B -1
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupMarathon")
        TNT1 A 0 A_JumpIfInventory("Clip",0,"PickupFail")
        TNT1 A 0 A_GiveInventory("Clip",5)
        stop

      PickupFail:
        CLIP B 1
        fail

      PickupMarathon:
        TNT1 A 0 A_JumpIfInventory("Clip",0,"PickupFail")
        TNT1 A 0 A_SetBlend("Green",0.4,4)
        TNT1 A 1 A_GiveInventory("Clip",10)
        stop
    }
}

actor Zombieman2 : Zombieman replaces Zombieman
{
    DropItem "Clip2"
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    +THRUGHOST
    +NOTDMATCH
    +NOICEDEATH
    states
    {
      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        POSS G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        POSS E 10 A_FaceTarget
        TNT1 A 0 A_PlaySound("grunt/attack")
        POSS F 8 A_CustomMissile("DoomEnemyBullet",32,0,random(-22.5,22.5),8)
        POSS E 8
        Goto See

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        POSS G 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        POSS G 16 A_Pain
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        POSS G 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        POSS G 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        POSS G 3 A_SetTranslucent(0.9,0)
        POSS G 0 A_Scream
        POSS G 3 A_SetTranslucent(0.8,0)
        POSS G 0 A_NoBlocking
        POSS G 3 A_SetTranslucent(0.7,0)
        POSS G 3 A_SetTranslucent(0.6,0)
        POSS G 3 A_SetTranslucent(0.5,0)
        POSS G 3 A_SetTranslucent(0.4,0)
        POSS G 3 A_SetTranslucent(0.3,0)
        POSS G 3 A_SetTranslucent(0.2,0)
        POSS G 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        POSS K 5
        POSS JIH 5
        Goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        POSS A 3 A_SetTranslucent(0.2,0)
        POSS A 3 A_SetTranslucent(0.3,0)
        POSS A 3 A_SetTranslucent(0.4,0)
        POSS A 3 A_SetTranslucent(0.5,0)
        POSS A 3 A_SetTranslucent(0.6,0)
        POSS A 3 A_SetTranslucent(0.7,0)
        POSS A 3 A_SetTranslucent(0.8,0)
        POSS A 3 A_SetTranslucent(0.9,0)
        POSS A 3 A_SetTranslucent(1,0)
        POSS A 1 A_SetShootable
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        POSS G 0 A_GenericFreezeDeath
        POSS G 70 A_UnsetSolid
        POSS G 0 A_IceGuyDie
        POSS G 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}
