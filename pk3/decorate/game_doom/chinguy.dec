actor ChingunDrop: SamsaraSlotFourDrop
{
    States
    {
      Spawn:
        MGUN A -1
        stop
    }
}

actor ChaingunGuy2 : ChaingunGuy replaces ChaingunGuy
{
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    +THRUGHOST
    +NOTDMATCH
    +NOICEDEATH
    dropitem "ChingunDrop"
	BloodType "SamsaraBloodyHellBlood"
    states
    {
      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 ACS_ExecuteAlways(680,0,2)
        goto "Super::Death"

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        CPOS G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 ACS_ExecuteAlways(680,0,1)
        goto "Super::XDeath"

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        CPOS E 10 A_FaceTarget
        TNT1 A 0 A_PlaySound("chainguy/attack")
        CPOS F 4 bright A_CustomMissile("DoomEnemyBullet",32,0,random(-22.5,22.5),8)
        TNT1 A 0 A_PlaySound("chainguy/attack")
        CPOS E 4 bright A_CustomMissile("DoomEnemyBullet",32,0,random(-22.5,22.5),8)
        CPOS F 1 A_CPosRefire
        goto Missile+2

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        CPOS G 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        CPOS G 16 A_Pain
        Goto See
		
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        CPOS G 5 A_FaceTarget
		TNT1 A 0 A_Gravity
        CPOS G 30 A_Pain
        CPOS G 70
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CPOS G 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        CPOS G 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        CPOS G 3 A_SetTranslucent(0.9,0)
        CPOS G 0 A_Scream
        CPOS G 3 A_SetTranslucent(0.8,0)
        CPOS G 0 A_NoBlocking
        CPOS G 3 A_SetTranslucent(0.7,0)
        CPOS G 3 A_SetTranslucent(0.6,0)
        CPOS G 3 A_SetTranslucent(0.5,0)
        CPOS G 3 A_SetTranslucent(0.4,0)
        CPOS G 3 A_SetTranslucent(0.3,0)
        CPOS G 3 A_SetTranslucent(0.2,0)
        CPOS G 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        CPOS NMLKJIH 5
        goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        CPOS A 3 A_SetTranslucent(0.2,0)
        CPOS A 3 A_SetTranslucent(0.3,0)
        CPOS A 3 A_SetTranslucent(0.4,0)
        CPOS A 3 A_SetTranslucent(0.5,0)
        CPOS A 3 A_SetTranslucent(0.6,0)
        CPOS A 3 A_SetTranslucent(0.7,0)
        CPOS A 3 A_SetTranslucent(0.8,0)
        CPOS A 3 A_SetTranslucent(0.9,0)
        CPOS A 3 A_SetTranslucent(1,0)
        CPOS A 1 A_SetShootable
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        //TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("ChaingunGuyFrozen")
        CPOS G 0 A_GenericFreezeDeath
        CPOS G 70 A_UnsetSolid
        CPOS G 0 A_IceGuyDie
        CPOS G 1 A_IceGuyDie
        TNT1 A -1
        stop
		
	  XDeathNashgore:
	    TNT1 A 0
	    TNT1 A 0 A_SpawnItemEx("NashGore_Gib9",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33) //A_SpawnDebris("NashGore_Gib9",0)
	    TNT1 A 0 A_SpawnItemEx("NashGore_Gib10",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33) //A_SpawnDebris("NashGore_Gib10",0)
	    TNT1 AA 0 A_SpawnItemEx("NashGore_Gib11",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33) //A_SpawnDebris("NashGore_Gib11",0)
	    TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        CPOS M 5
	    TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        CPOS N 5 A_XScream
	    TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        CPOS O 5 A_NoBlocking
	    TNT1 AA 0 A_SpawnItemEx("NashGore_GibGenerator",0,0,0,0,0,0,0,33)
        CPOS PQRST 5
        CPOS U -1
        stop
		
      DeathBrutal:
	    TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
		TNT1 AAAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
		TNT1 AAAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
		TNT1 AAA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        goto "Super::Death"
		
	  XDeathBrutal:
	    CPOS M 1
		TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy1",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy2",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("BrutalChunkBouncy3",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("BrutalBrownOrgan",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("BrutalRedOrgan",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 AAAA 0 A_SpawnItemEx("BrutalChunk",random(-10,10),random(-10,10),random(12,40),random(-500,500)/100.00,random(-500,500)/100.00,random(300,1000)/100.00,random(-180,180),160)
		TNT1 AA 0 A_SpawnItemEx("BrutalChunkHuge",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 A 0 A_SpawnItemEx("BrutalSkull",0,0,56,random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		TNT1 AAAA 0 A_SpawnItemEx("BrutalTinyBouncyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("RedMist",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
		TNT1 AAAAA 0 A_SpawnItemEx("RedMistIntense",random(-10,10),random(-10,10),random(12,40),random(-800,800)/100.00,random(-800,800)/100.00,random(600,1200)/100.00,random(-180,180),160)
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160)
		SKGB A 1 A_SpawnItemEx("BrutalOnlineSound",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work. Weird!
		SKGB A 8 A_XScream
        SKGB A 5 A_NoBlocking
        SKGB BCDEF 5
        SKGB G -1
		stop
    }
}
