actor Archvile2 : Archvile replaces Archvile
{
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "DragonClaw", 1.6
    PainChance "MightyBoot", 255
    
    +FIREDAMAGE
    +DONTRIP
    +NOTDMATCH
    +NOICEDEATH
    states
    {
      Death.DukeExplosive:
      Death.DukeSpectral:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
        goto "Super::Death"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        VILE Q 2 A_FaceTarget
        TNT1 A 0 A_Recoil(12)
        VILE Q 16 A_Pain
        Goto See
        
      Pain.MarathonStun:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        VILE Q 5 A_FaceTarget
        VILE Q 30 A_Pain
        VILE Q 70
        Goto See

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Missile"

      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        VILE Q 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        VILE Q 4 A_SpawnItem("ZorchEffectBig")
        VILE Q 4 A_SetTranslucent(0.9,0)
        VILE Q 0 A_Scream
        VILE Q 4 A_SetTranslucent(0.8,0)
        VILE Q 0 A_NoBlocking
        VILE Q 4 A_SetTranslucent(0.7,0)
        VILE Q 4 A_SetTranslucent(0.6,0)
        VILE Q 4 A_SetTranslucent(0.5,0)
        VILE Q 4 A_SetTranslucent(0.4,0)
        VILE Q 4 A_SetTranslucent(0.3,0)
        VILE Q 4 A_SetTranslucent(0.2,0)
        VILE Q 4 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("ArchvileFrozen")
        VILE Q 0 A_GenericFreezeDeath
        VILE Q 70 A_UnsetSolid
        VILE Q 0 A_IceGuyDie
        VILE Q 1 A_IceGuyDie
        TNT1 A -1
        stop
        
      DeathBrutal:
        TNT1 A 0
        TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work, so I have to spawn the item. Weird!
        goto "Super::Death"
        
      DeathHappyfun:
        TNT1 A 0
        TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto "Super::Death"
    }
}