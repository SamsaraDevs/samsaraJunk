actor ShotgunDrop: SamsaraSlotTwoDrop
{
    States
    {
      Spawn:
        SHOT A -1
        stop
    }
}

actor ShotgunGuy2 : ShotgunGuy replaces ShotgunGuy
{
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    +THRUGHOST
    +NOTDMATCH
    +NOICEDEATH
    dropitem "ShotgunDrop"
    states
    {
      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        SPOS G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        SPOS E 10 A_FaceTarget
        TNT1 A 0 A_PlaySound("shotguy/attack")
        TNT1 AAA 0 bright A_CustomMissile("DoomEnemyBullet",32,0,random(-22.5,22.5),8)
        SPOS F 10 bright
        SPOS E 10
        goto See

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        SPOS G 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        SPOS G 16 A_Pain
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        SPOS G 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        SPOS G 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        SPOS G 3 A_SetTranslucent(0.9,0)
        SPOS G 0 A_Scream
        SPOS G 3 A_SetTranslucent(0.8,0)
        SPOS G 0 A_NoBlocking
        SPOS G 3 A_SetTranslucent(0.7,0)
        SPOS G 3 A_SetTranslucent(0.6,0)
        SPOS G 3 A_SetTranslucent(0.5,0)
        SPOS G 3 A_SetTranslucent(0.4,0)
        SPOS G 3 A_SetTranslucent(0.3,0)
        SPOS G 3 A_SetTranslucent(0.2,0)
        SPOS G 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        SPOS LKJIH 5
        goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        SPOS A 3 A_SetTranslucent(0.2,0)
        SPOS A 3 A_SetTranslucent(0.3,0)
        SPOS A 3 A_SetTranslucent(0.4,0)
        SPOS A 3 A_SetTranslucent(0.5,0)
        SPOS A 3 A_SetTranslucent(0.6,0)
        SPOS A 3 A_SetTranslucent(0.7,0)
        SPOS A 3 A_SetTranslucent(0.8,0)
        SPOS A 3 A_SetTranslucent(0.9,0)
        SPOS A 3 A_SetTranslucent(1,0)
        SPOS A 1 A_SetShootable
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        //TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("ShotgunGuyFrozen")
        SPOS G 0 A_GenericFreezeDeath
        SPOS G 70 A_UnsetSolid
        SPOS G 0 A_IceGuyDie
        SPOS G 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}
