actor Arachnotron2 : Arachnotron replaces Arachnotron
{
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
    
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
    states
    {
      Death.DukeExplosive:
      Death.DukeSpectral:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
        goto "Super::Death"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        BSPI I 2 A_FaceTarget
        TNT1 A 0 A_Recoil(8)
        BSPI I 16 A_Pain
        goto See
        
      Pain.MarathonStun:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        BSPI I 5 A_FaceTarget
        BSPI I 30 A_Pain
        BSPI I 70
        Goto See

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Missile"

      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        BSPI I 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        BSPI I 4 A_SpawnItem("ZorchEffectWide")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        BSPI I 4 A_SetTranslucent(0.9,0)
        BSPI I 0 A_Pain
        BSPI I 4 A_SetTranslucent(0.8,0)
        BSPI I 0 A_NoBlocking
        BSPI I 4 A_SetTranslucent(0.7,0)
        BSPI I 4 A_SetTranslucent(0.6,0)
        BSPI I 4 A_SetTranslucent(0.5,0)
        BSPI I 4 A_SetTranslucent(0.4,0)
        BSPI I 4 A_SetTranslucent(0.3,0)
        BSPI I 4 A_SetTranslucent(0.2,0)
        BSPI I 4 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1 A_BossDeath
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        BSPI PONMLKJ 5
        goto See+1

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        BSPI A 3 A_SetTranslucent(0.2,0)
        BSPI A 3 A_SetTranslucent(0.3,0)
        BSPI A 3 A_SetTranslucent(0.4,0)
        BSPI A 3 A_SetTranslucent(0.5,0)
        BSPI A 3 A_SetTranslucent(0.6,0)
        BSPI A 3 A_SetTranslucent(0.7,0)
        BSPI A 3 A_SetTranslucent(0.8,0)
        BSPI A 3 A_SetTranslucent(0.9,0)
        BSPI A 3 A_SetTranslucent(1,0)
        BSPI A 1 A_SetShootable
        Goto See+1

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItemEx("ArachnotronFrozen",0,0,0,0,0,0,0,32)
        //BSPI I 0 A_GenericFreezeDeath
        //BSPI I 70 A_UnsetSolid
        //BSPI I 0 A_IceGuyDie
        //BSPI I 1 A_IceGuyDie
        TNT1 A -1 A_BossDeath
        stop
        
      DeathBrutal:
        TNT1 A 0
        TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work, so I have to spawn the item. Weird!
        goto "Super::Death"
        
      DeathHappyfun:
        TNT1 A 0
        TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto "Super::Death"
    }
}

actor ArachnotronFrozen
{
  Health 10
  Radius 64
  Height 64
  Mass 600
  Monster
  +FLOORCLIP
  +NEVERRESPAWN
  +NOTELEPORT
  +NOTARGET
  States
  {
  Spawn:
    BSPI I 1
    BSPI I 0 A_GenericFreezeDeath
    BSPI I 70 A_UnsetSolid
  Death:
    BSPI I 0 A_IceGuyDie
    BSPI I 1 A_IceGuyDie
    TNT1 A -1
    stop
}
}

actor ArachnotronPlasma2 : ArachnotronPlasma replaces ArachnotronPlasma
{
    DamageType "DoomSpiderPlasma"
}

