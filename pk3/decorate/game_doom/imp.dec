actor DoomImp2 : DoomImp replaces DoomImp
{
    +NOTDMATCH
    +NOICEDEATH
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    states
    {
      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Death.DukeExplosive:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        TROO H 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TROO H 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TROO H 16 A_Pain
        Goto See

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Missile"

      Death.ZorchPistol:

      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TROO H 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        TROO H 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TROO H 3 A_SetTranslucent(0.9,0)
        TROO H 0 A_Scream
        TROO H 3 A_SetTranslucent(0.8,0)
        TROO H 0 A_NoBlocking
        TROO H 3 A_SetTranslucent(0.7,0)
        TROO H 3 A_SetTranslucent(0.6,0)
        TROO H 3 A_SetTranslucent(0.5,0)
        TROO H 3 A_SetTranslucent(0.4,0)
        TROO H 3 A_SetTranslucent(0.3,0)
        TROO H 3 A_SetTranslucent(0.2,0)
        TROO H 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        TROO ML 8
        TROO KJI 6
        goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        TROO A 3 A_SetTranslucent(0.2,0)
        TROO A 3 A_SetTranslucent(0.3,0)
        TROO A 3 A_SetTranslucent(0.4,0)
        TROO A 3 A_SetTranslucent(0.5,0)
        TROO A 3 A_SetTranslucent(0.6,0)
        TROO A 3 A_SetTranslucent(0.7,0)
        TROO A 3 A_SetTranslucent(0.8,0)
        TROO A 3 A_SetTranslucent(0.9,0)
        TROO A 3 A_SetTranslucent(1,0)
        TROO A 1 A_SetShootable
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("ImpFrozen")
        TROO H 0 A_GenericFreezeDeath
        TROO H 70 A_UnsetSolid
        TROO H 0 A_IceGuyDie
        TROO H 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}

actor DoomImpBall2 : DoomImpBall replaces DoomImpBall
{
    +THRUGHOST
    +FIREDAMAGE
}
