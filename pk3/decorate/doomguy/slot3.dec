actor "Super Shotgun" : SuperShotgun
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 3
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "%k \cdsmeared\c- %o \cdacross the wall with the SSG.\c-"
    States
    {
      Ready:
        DSSG A 1 A_WeaponReady
        Loop

      Deselect:
        DSSG A 1 A_Lower
        Loop

      Select:
        DSSG A 1 A_Raise
        Loop

      Fire:
        DSSG A 3
        TNT1 A 0 A_PlayWeaponSound ("doomguy/sshotf")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireBullets (11.2, 7.1, 20, 5, "DoomBulletPuff")
        goto FireEnd

      CoopMid:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("DoomBullet", random(-1120, 1120)/100.00, 0, 0, 1, 0, random(-710, 710)/100.00)
        TNT1 A                   0 A_FireCustomMissile("DoomBullet", random(-1120, 1120)/100.00, 1, 0, 1, 0, random(-710, 710)/100.00)
        goto FireEnd

      FireEnd:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageEnd")
        DSSG A 2 bright A_ZoomFactor(0.985, ZOOM_INSTANT)
        DSSG A 4 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSSG A 3
        DSSG B 2
        DSSG C 3
        DSSG DE 4 A_CheckReload
        DSSG F 2 A_PlayWeaponSound("doomguy/sshoto")
        DSSG GHIJ 2
        DSSG K 3
        DSSG L 3 A_PlayWeaponSound("doomguy/sshotl")
        DSSG MNOP 2
        DSSG Q 1
        DSSG Q 1 A_PlayWeaponSound("doomguy/sshotc")
        DSSG RST 3 A_ReFire
        DSSG A 2 A_Refire
        goto Ready

      RageEnd:
        DSSG A 2 bright
        DSSG A 4 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSSG A 4
        DSSG BCC 2
        DSSG DE 4 A_CheckReload
        DSSG F 2 A_PlayWeaponSound("doomguy/sshoto")
        DSSG GHIJJK 2
        DSSG L 4 A_PlayWeaponSound("doomguy/sshotl")
        DSSG MNOOP 2
        DSSG Q 2 A_PlayWeaponSound("doomguy/sshotc")
        DSSG RSSTTA 2 A_Refire
        goto Ready

      VanillaFire:
        DSSG A 7 bright
        DSG2 B 7
        DSG2 C 7 A_CheckReload
        DSG2 D 7 A_PlayWeaponSound("doomguy/sshoto")
        DSG2 E 7
        DSG2 F 7 A_PlayWeaponSound("doomguy/sshotl")
        DSG2 G 6
        TNT1 A 0 A_PlayWeaponSound("doomguy/sshotc")
        DSG2 H 6 A_Refire
        DSSG A 5 A_ReFire
        Goto Ready

      Flash:
        DSSF A 3 bright A_Light1
        DSSF B 3 bright A_Light2
        Goto LightDone

      Spawn:
        WSSG A -1
        Stop
    }
}
