actor DoomguyPlayer : PlayerPawn
{
    Player.DisplayName "Doomguy"
    Player.CrouchSprite "DGYC"
    Player.Face "DGY"
    BloodColor "Red"
    Player.StartItem " Pistol ", 1
    Player.StartItem " Fist ", 1
    Player.StartItem "DoomguyClass", 1
    Player.StartItem "IsSamsaraClass", 1
    Player.StartItem "Clip", 50
    Player.Soundclass "Doomguy"
    Player.JumpZ 9//Default is 8
    DamageFactor "PoisonCloud", 1.75
    //DamageFactor "Wraithverge", 0.75
    DamageFactor "WMCBeam", 0.75
    DamageFactor "WMCNade", 0.75
    DamageFactor "MightyBoot", 1.5
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.1
	DamageFactor "EnemyInfightDamage", 0.0
    +NOSKIN
    -DONTBLAST
    -THRUSPECIES
    Species "Player"
    Player.ColorRange 112, 127
    States
    {
      Spawn:
        DGUY A -1
        Loop

      See:
        DGUY ABCD 4 
        Loop

      Missile:
        DGUY E 12 
        Goto Spawn

      Melee:
        DGUY F 6 BRIGHT
        Goto Missile

      Pain:
        DGUY G 4 
        DGUY G 4 A_Pain
        Goto Spawn

      Death.HexGreenFire:
      Death.QuakeFire:
      Burn:
        TNT1 A 0 A_PlaySound("*burndeath")
        DBRN ABCDE 5 Bright
        DBRN FFGHHIIJJK 3 Bright
        DBRN LMNOP 5 Bright
        DBRN QR 4 Bright
        DBRN STU 6
        DBRN V -1
        stop

      Death:
        DGUY H 10
        DGUY I 10 A_PlayerScream
        DGUY J 10 A_NoBlocking
        DGUY KLM 10
        DGUY N -1
        Stop

      XDeath:
        DGUY O 5
        DGUY P 5 A_XScream
        DGUY Q 5 A_NoBlocking
        DGUY RSTUV 5
        DGUY W -1
        Stop

      Death.CorvusGrimBallista:
        DGUY O 5
        DGUY P 5 A_PlaySoundEx("*falling",CHAN_VOICE,0,0)
        DGUY Q 5 A_NoBlocking
        DGUY RSTUV 5
        DGUY W -1
        Stop

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        DGUY G 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        DGUY G 3 A_SpawnItem("ZorchEffectSmall")
        DGUY G 3 A_SetTranslucent(0.9,0)
        DGUY G 0 A_PlayerScream
        DGUY G 3 A_SetTranslucent(0.8,0)
        DGUY G 0 A_NoBlocking
        DGUY G 3 A_SetTranslucent(0.7,0)
        DGUY G 3 A_SetTranslucent(0.6,0)
        DGUY G 3 A_SetTranslucent(0.5,0)
        DGUY G 3 A_SetTranslucent(0.4,0)
        DGUY G 3 A_SetTranslucent(0.3,0)
        DGUY G 3 A_SetTranslucent(0.2,0)
        DGUY G 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop

      CoOpModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 A_ChangeFlag("DONTBLAST",1)
        DGUY A 1
        goto Spawn

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTauntDoomguy",1)
        goto Death

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-5,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
	DGUY G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeDMTauntDoomguy",1)
        goto XDeath

      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTauntDoomguy",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
	TNT1 A 0 A_PlayWeaponSound("misc/freeze")
        DGUY Z 70 A_NoBlocking//A_GenericFreezeDeath
        DGUY Z 0 A_SetTranslucent(0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(8,48),random(-2,2),random(-2,2),random(1,6))
        DGUY Z 1 A_PlayWeaponSound("misc/icebreak")//A_FreezeDeathChunks
        DGUY Z -1
        stop
        //DGUY G 5 A_GenericFreezeDeath
        //DGUY G 1 A_FreezeDeathChunks
        //Wait

      Pain.MightyBoot:
	    DGUY G 1 ACS_ExecuteAlways(312, 0, 25, 5, 24)
        DGUY G 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        DGUY G 6 A_Pain
        Goto See
		
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
	    DGUY G 1 ACS_ExecuteAlways(312, 0, 25, 5, 100)
		TNT1 A 0 A_GiveInventory("NothingSpeed")
		TNT1 A 0 A_SetBlend("White",0.9,105)
        DGUY G 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        DGUY G 6 A_Pain
        Goto See
    }
}

actor DoomguyClass : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }
actor VanillaDoom : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }

actor DoomBulletPuff : BulletPuff
{
    States
    {
      Spawn:
        DPFF A 4 bright
        DPFF B 4

      Melee:
        DPFF CD 4
        Stop
    }
}

actor DoomBullet : FastProjectile
{
    Damage (Random(1,3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("DoomBulletPuff")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}
