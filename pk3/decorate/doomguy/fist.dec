actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "%k \cdfisted\c- %o \cdwith a pair of brass knuckles.\c-"
    States
    {
      Ready:
        DPUN A 1 A_WeaponReady
        loop

      Deselect:
        DPUN A 1 A_Lower
        loop

      Select: 
        DPUN A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "Rage")
        DPUN LBCD 1
        goto Fire2

      VanillaFire:
        DPUN M 4
        goto Fire2

      Rage:
        DPUN LC 2
        goto Fire2

      Fire2:
        DPUN E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk")
        DPUN E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DPUN E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff")
        goto FireEnd

      FireBerserk:
        DPUN E 0 A_CustomPunch(random(10, 20)*12, 1, 0, "DoomPunchPuff2") // Technically should be random(1, 10)*20, but oh well.
        goto FireEnd

      FirePDBerserk:
        DPUN E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "DoomPunchPuff2") // Punchdrunk triples fist damage already.
        goto FireEnd

      FireEnd:
        DPUN E 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DPUN E 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "RageEnd")
        DPUN E 1
        DPUN FGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready
    
      VanillaEnd:
        DPUN N 4
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready

      RageEnd:
        DPUN EFGHIJL 2
        DPUN A 4 A_ReFire
        goto Ready
    }
}

actor DoomPunchPuff : BulletPuff
{
    +PUFFONACTORS
    DamageType "DoomFist"
    States
    {
      Crash:
	    TNT1 A 0
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        Stop

      Spawn:
      Melee:
	    TNT1 A 0
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff2 : BulletPuff
{
    +PUFFONACTORS
    DamageType "DoomFist"
    States
    {
      Crash:
	    TNT1 A 0
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Spawn:
      Melee:
	    TNT1 A 0
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}
