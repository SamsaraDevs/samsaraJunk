actor " Pistol " : Pistol
{
    Decal BulletChip
    Weapon.SlotNumber 1  
    Weapon.SlotPriority 1
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    AttackSound "doomguy/pistol"
    DamageType Pistol
    Obituary "%k \cdexecuted\c- %o \cdfor insubordination.\c-"
    States
    {
      Ready:
        DPIS A 1 A_WeaponReady
        Loop

      Deselect:
        DPIS A 1 A_Lower
        Loop

      Select:
        DPIS A 1 A_Raise
        Loop

      Fire:
        DPIS A 4
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "DoomBulletPuff2")
        goto FireAnim

      CoopFire:
        TNT1 A 0 A_FireCustomMissile("DoomBullet2", 0, 1, 0, 1)
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        Goto FireAnim

      FireAnim:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
    	TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "RageAnim")
        DPIS B 2 bright
        DPIS C 1 bright
        DPIS CEEDDBB 1
        DPIS A 5 A_ReFire
        Goto Ready

      RageAnim:
        DPIS BC 1 bright
        DPIS EDB 1
        DPIS A 3 A_Refire
        goto Ready
        
      VanillaFire:
        DPIS F 6 BRIGHT A_GunFlash
        DPIS G 4
        DPIS H 5 A_ReFire
        Goto Ready

      Hold:
        DPIS A 4
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TNT1 A 0 A_FireBullets (5.6, 0, -1, 5, "DoomBulletPuff2")
        Goto FireAnim
        
      CoopHold:
        TNT1 A 0 A_FireCustomMissile("DoomBullet2", random(-560,560)/100.00, 1, 0, 1)
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        Goto FireAnim
        
      Flash:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFlash")
        DPSF A 3 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone
	  
	  VanillaFlash:
        TNT1 A 3 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone

      Spawn:
        WPST A -1
        Stop
    }
}

actor DoomBulletPuff2 : DoomBulletPuff { DamageType Pistol }

actor DoomBullet2: FastProjectile
{
    Damage (Random(1, 3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    DamageType "Pistol"
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("DoomBulletPuff2")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}
