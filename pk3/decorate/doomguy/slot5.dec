actor "Rocket Launcher" : RocketLauncher
{
    Weapon.SlotNumber 5
    +INVENTORY.UNDROPPABLE
    States
    {
      Ready:
        DMIS A 1 A_WeaponReady
        Loop

      Deselect:
        DMIS A 1 A_Lower
        Loop

      Select:
        DMIS A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        DMIS A 2 
        DMIS A 4 bright A_GunFlash
        DMIS B 2 bright 
        goto FireMid
      
      VanillaFire:
        DRKL A 3 bright A_Light1
        DRKL B 4 bright
        DRKL C 1 bright A_Light2
        goto FireMid

      FireMid:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireCustomMissile("DoomRocket")
        goto FireEnd

      CoopMid:
        TNT1 A 0 A_FireCustomMissile("DoomRocket2")
        goto FireEnd
      
      FireEnd:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DMIS D 3 bright
        DMIS C 3 bright
        DMIS BE 2
        DMIS FG 1
        DMIS A 0 A_Refire
        goto Ready
      
      RageEnd:
        DMIS DC 2 bright
        DMIS BFG 2
        DMIS A 0 A_Refire
        goto Ready
        
      VanillaEnd:
        DRKL C 3 bright
        DRKL D 4 bright
        DMIS B 5 A_Light0
        DMIS B 0 A_ReFire // 20 frames total
        Goto Ready
        
      Flash:
        DMSF A 3 bright A_Light1
        DMSF B 2 bright
        DMSF C 2 bright A_Light2
        DMSF DE 3 bright
        Goto LightDone

      Spawn:
        WDRL A -1
        Stop
    }
}

actor DoomRocket : Rocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
    -FIREDAMAGE
    Damagetype "Explosive"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT5"
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

actor DoomRocket2 : DoomRocket { +THRUSPECIES Species "Player" }
