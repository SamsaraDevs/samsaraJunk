actor "Elven Wand" : Weapon
{
    +BLOODSPLATTER
    +PUFFONactorS
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.SelectionOrder 2000
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickupMessage "You got the Elven Wand!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Decal RailScorchLower
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
    Weapon.YAdjust 5
    
    States
    {
      Ready:
        GWND A 1 A_WeaponReady
        Loop

      Deselect:
        GWND A 1 A_Lower
        Loop

      Select:
        GWND A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.6, 0, 1,random(2,4)+300, "GoldWandPuff3") // Previously random(1,8)+6
        goto FireAnim

      Hold:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Hold2:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.6, 0, -1,random(2,4)+300, "GoldWandPuff3") // Previously random(1,8)+6
        goto FireAnim

      FireTomed:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireTomed")
        TNT1 A 0 A_JumpIfInventory("SpectralFiring",1,"FireTomedSpectral")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX4",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",-5.6,0,0,1)
        goto FireAnimTomed

      CoopFire:
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("ElvenBullet",0,0,0,1)
        goto FireAnim

      CoopHold:
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("ElvenBullet",random(-560,560)/100.00,0,0,1)
        goto FireAnim

      CoopFireTomed:
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX42",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",-5.6,0,0,1)
        goto FireAnimTomed

      FireTomedSpectral:
        GWND B 3 BRIGHT ACS_ExecuteAlways(317, 0, 5)
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX43",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX43",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX43",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX43",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX43",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX33",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX33",-5.6,0,0,1)
        GWND C 4 BRIGHT
        GWND D 3
        GWND D 0 A_ClearRefire
        GWND D 0 A_ReFire
        Goto Ready
        
      CheckForAmmo:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
        
      CheckForAmmoHold:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Hold2")
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
        
      FireAnim:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
      FireAnim2:
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready
        
      FireAnimTomed:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmoTomed")
      FireAnimTomed2:
        GWND C 4 BRIGHT
        GWND D 3
        GWND D 0 A_ClearRefire
        GWND D 0 A_ReFire
        Goto Ready
        
      TakePistolAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto FireAnim2
        
      TakePistolAmmoTomed:
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto FireAnimTomed2
    }
}

actor ElvenBullet : FastProjectile
{
    Damage (random(1,8)+6)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "Pistol"
    Decal RailScorchLower
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("GoldWandPuff3")
        stop
    }
}

actor GoldWandPuff3 : GoldWandPuff1
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandPuff4 : GoldWandPuff2
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandFX3 : GoldWandFX2
{
    Decal HImpScorch
    DamageType "Pistol"
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
}

actor GoldWandFX32 : GoldWandFX3 { +THRUSPECIES Species "Player" }

actor GoldWandFX4 : FastProjectile
{
    Damage (Random (2, 12))
    Radius 2
    Height 2
    Speed 320
    Projectile
    DamageType "Pistol"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
    Decal RailScorchLower
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
      Crash:
        FX01 EFGH 3 BRIGHT
        Stop

      XDeath:
        TNT1 A 1
        stop
    }
}

actor GoldWandFX42 : GoldWandFX4 { +THRUSPECIES Species "Player" }

actor GoldWandFX43 : GoldWandFX4 { DamageType "Spectral" }

actor GoldWandFX33 : GoldWandFX3 { DamageType "Spectral" }
