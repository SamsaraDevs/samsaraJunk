actor "Elven Wand" : Weapon
{
    +BLOODSPLATTER
    +PUFFONactorS
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.SelectionOrder 2000
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickupMessage "You got the Elven Wand!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Decal RailScorchLower
    Obituary "%k \cqput an elven hex on\c- %o."
    Weapon.YAdjust 5
    
    States
    {
      Ready:
        GWND A 1 A_WeaponReady
        Loop

      Deselect:
        GWND A 1 A_Lower
        Loop

      Select:
        GWND A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.6, 0, 1,random(2,4)+3, "GoldWandPuff3") // Previously random(1,8)+6
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        Goto Ready

      Hold:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.6, 0, -1,random(2,4)+3, "GoldWandPuff3") // Previously random(1,8)+6
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready

      FireTomed:
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX4",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",-5.6,0,0,1)
        GWND C 4 BRIGHT
        GWND D 3
        GWND D 0 A_ClearRefire
        GWND D 0 A_ReFire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"CoopFireTomed")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("ElvenBullet",0,0,0,1)
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready

      CoopHold:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"CoopFireTomed")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("ElvenBullet",random(-5.6,5.6),0,0,1)
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready

      CoopFireTomed:
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX42",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",-5.6,0,0,1)
        GWND C 4 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        Goto Ready
    }
}

actor ElvenBullet : FastProjectile
{
    Damage (random(1,8)+6)
    Radius 1
    Height 1
    Speed 300
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "Pistol"
    Decal RailScorchLower
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("GoldWandPuff3")
        stop
    }
}

actor GoldWandPuff3 : GoldWandPuff1
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandPuff4 : GoldWandPuff2
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandFX3 : GoldWandFX2
{
    Decal HImpScorch
    DamageType "Pistol"
    Obituary "%k put an elven hex on %o."
}

actor GoldWandFX32 : GoldWandFX3 { +THRUSPECIES Species "Player" }

actor GoldWandFX4 : FastProjectile
{
    Damage (Random (2, 12))
    Radius 2
    Height 2
    Speed 300
    Projectile
    DamageType "Pistol"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    Obituary "%k \cqput an elven hex on\c- %o."
    Decal RailScorchLower
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
      Crash:
        FX01 EFGH 3 BRIGHT
        Stop

      XDeath:
        TNT1 A 1
        stop
    }
}

actor GoldWandFX42 : GoldWandFX4 { +THRUSPECIES Species "Player" }
