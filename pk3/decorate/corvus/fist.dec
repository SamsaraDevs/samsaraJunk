actor " Staff " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3800
    +WIMPY_WEAPON
    +MELEEWEAPON
    +NOALERT
    +STAFF2_KICKBACK
    +INVENTORY.UNDROPPABLE
	+THRUGHOST
    Obituary "$SAMSARA_CORVUS_OB_FIST"
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"Ready2")
        STFF A 1 A_WeaponReady
        Loop

      Ready2:
        TNT1 A 0 A_Jump(128,"Ready3")
        STFF A 0 A_PlayWeaponSound("heretic/staffcrackle")

      Ready3:
        STFF DEF 4 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"Deselect2")
        STFF A 1 A_Lower
        Loop

      Deselect2:
        STFF D 1 A_Lower
        Loop

      Select:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"Select2")
        STFF A 1 A_Raise
        Loop

      Select2:
        STFF D 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        STFF B 6
        STFF C 8 A_CustomPunch(random(5,20),1,0,"StaffPuff3")//A_StaffAttack(random[StaffAttack](5, 20), "StaffPuff")
        STFF B 8 A_ReFire
        Goto Ready

      FireTomed:
	    TNT1 A 0 A_JumpIfInventory("SpectralFiring",1,"FireSpectral")
        STFF G 6
        STFF H 8 A_CustomPunch(random(60,120),1,0,"StaffPuff4")//A_StaffAttack(random[StaffAttack](18, 81), "StaffPuff2")
        STFF G 8 A_ReFire
        Goto Ready2

      FireSpectral:
        STFF G 6
        STFF H 8 A_CustomPunch(random(60,120),1,0,"StaffPuff5") // Yeah, good luck meleeing them...
        STFF G 8 A_ReFire
        Goto Ready2

    }
}

actor StaffPuff3 : StaffPuff
{
    DamageType "Pistol"
    SeeSound "heretic/staffhit"
    States
    {
      Spawn:
      Crash:
      Melee:
        TNT1 A 0 A_PlaySound("heretic/staffhit")
        PUF3 A 4 BRIGHT
        PUF3 BCD 4
        Stop
    }
}

actor StaffPuff4 : StaffPuff2
{
    DamageType "Pistol"
    SeeSound "heretic/staffpowerhit"
    States
    {
      Spawn:
      Crash:
      Melee:
        TNT1 A 0 A_PlaySound("heretic/staffpowerhit")
        PUF4 ABCDEF 4 BRIGHT
        Stop
    }
}

actor StaffPuff5 : StaffPuff4
{
    DamageType "Spectral"
}
