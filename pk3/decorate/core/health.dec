actor FiniteHealth: Health
{
    Inventory.Amount 1
}

actor FiniteHealth200: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 200
}

actor FiniteHealth300: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 300
}

actor InfiniteHealth: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 0x7FFFFFFF
}

actor HealPack0: CustomInventory replaces HealthBonus
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.25
    +FLOATBOB
    -INVENTORY.ALWAYSPICKUP
    -COUNTITEM

    Inventory.PickupMessage "$SAMSARA_HEALPACK0"
    Inventory.PickupSound "healpack0pickup"

    States
    {
      Spawn:
        MEDI A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,32,0,0) == 1, "Nope")
        MEDI A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth200", 1)
        stop

      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 1)
        stop
        
      Nope:
        TNT1 A 1
        stop
    }
}

actor HealPack1: CustomInventory replaces StimPack
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.5
    +FLOATBOB
    Inventory.PickupMessage "$SAMSARA_HEALPACK1"
    Inventory.PickupSound "healpack1pickup"

    states
    {
      Spawn:
        MED2 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,32,0,0) == 1, "Nope")
        MED2 A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth", 10)
        stop

      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 10)
        stop
        
      Nope:
        TNT1 A 1
        stop
    }
}

actor HealPack2 : CustomInventory replaces Medikit
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.75
    +FLOATBOB

    Inventory.PickupMessage "$SAMSARA_HEALPACK2"
    Inventory.PickupSound "silence"

    states
    {
      Spawn:
        MED3 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,32,0,0) == 1, "Nope")
        MED3 A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "PickupHexen")
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupHeretic")

        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_JumpIfInventory("RuneProsperity", 1, "PickupProsperity")
        TNT1 A 0 A_JumpIfHealthLower(100, "PickupSuccess")
        goto PickupFail
        
      PickupHeretic:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_JumpIfInventory("PortQuartzFlask", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("PortQuartzFlask")
        stop
        
      PickupHexen:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_JumpIfInventory("PortQuartzFlask2", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("PortQuartzFlask2")
        stop

      PickupProsperity:
        TNT1 A 1 A_JumpIfHealthLower(250, "PickUpSuccess")
        goto PickupFail

      PickupSuccess:
        TNT1 A 1 A_GiveInventory("FiniteHealth", 25)
        goto PickupSound

      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 25)
        goto PickupSound

      InfiniteStop:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 25)
        stop

      PickupSound:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "HereticSound")
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "HexenSound")
        TNT1 A 0 A_PlaySound("healpack2pickup")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "DukeSound")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "MarathonSound")
        stop
        

      HereticSound:
        TNT1 A 0 A_PlaySound("heretic/itemget")
        stop

      HexenSound:
        TNT1 A 0 A_PlaySound("hexen/artiget")
        stop
        
      DukeSound:
        TNT1 A 0
        TNT1 A 0 A_JumpIfHealthLower(50, 1)
        stop
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("DukeTauntCooldown", 1, "PickupStop")
        TNT1 A 0 A_JumpIfInventory("DukeBallgag", 1, "PickupStop")
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown", 5)
        TNT1 A 0 A_PlaySoundEx("duke/criticalheal", "soundslot7", 0, 0)
        stop
        
      MarathonSound:
        TNT1 A 0 A_SetBlend("Green", 0.3, 3)
        stop

      PickupFail:
        MED3 A 1 BRIGHT
        fail
        
      PickupStop:
        TNT1 A 0
        stop
        
      Nope:
        TNT1 A 1
        stop
    }
}

actor SuperHealth : CustomInventory replaces Soulsphere
{
    RenderStyle Add
    Alpha 0.95
    +FLOATBOB
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +COUNTITEM
    Inventory.PickupAnnouncerEntry "soulsphere"
    Inventory.PickupMessage "$SAMSARA_HEALPACK3"
    Inventory.PickupSound "nosoundforyoubitch"
    states
    {
      Spawn:
        SOUL A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,32,0,0) == 1, "Nope")
        SOUL A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupHeretic")
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "PickupHexen")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupDuke")
        TNT1 A 0 A_JumpIfInventory("QuakeClass", 1, "PickupQuake")
        goto PickupSuccess

      PickupHeretic:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_JumpIfInventory("PortSuperHealth", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("PortSuperHealth")
        stop

      PickupHexen:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_JumpIfInventory("PortSuperHealth2", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("hexen/artiget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("PortSuperHealth2")
        stop

      PickupSuccess:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth200", 100)
        goto PickupSound

      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 100)
        goto PickupSound

      PickupDuke:
        TNT1 A 0
        TNT1 A 0 A_JumpIfHealthLower(25, 1)
        goto PickupDuke2
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("DukeTauntCooldown", 1, "PickupDuke2")
        TNT1 A 0 A_JumpIfInventory("DukeBallgag", 1, "PickupDuke2")
        TNT1 A 0 A_PlaySoundEx("duke/criticalheal", "soundslot7", 0, 0)
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown", 5)
        TNT1 A 0 ACS_ExecuteAlways(205)
      PickupDuke2:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "InfiniteDuke")
        TNT1 A 1 A_GiveInventory("FiniteHealth200", 50)
        goto PickupSound

      InfiniteDuke:
        TNT1 A 1 A_GiveInventory("InfiniteHealth", 50)
        goto PickupSound

      PickupQuake:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 ACS_ExecuteAlways(259, 0, 100, 1, 175)
        goto PickupSound

      PickupSound:
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "HereticSound")
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "HexenSound")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "DukeSound")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "MarathonSound")
        TNT1 A 0 A_JumpIfInventory("QuakeClass", 1, "QuakeSound")
        TNT1 A 0 A_PlaySound("healpack3pickup", CHAN_ITEM)
        stop

      HereticSound:
        TNT1 A 0 A_PlaySound("heretic/itemget", CHAN_ITEM)
        stop

      HexenSound:
        TNT1 A 0 A_PlaySound("hexen/artiget", CHAN_ITEM)
        stop

      DukeSound:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("duke/atomhealth", CHAN_ITEM)
        stop

      MarathonSound:
        TNT1 A 0 A_SetBlend("F0F8FF", 0.5, 7) // Light Alice Blue
        TNT1 A 0 A_PlaySound("marathon/bigitemget", CHAN_ITEM)
        stop

      QuakeSound:
        TNT1 A 0 A_PlaySound("quakeweps/megahealth", CHAN_ITEM)
        stop


      PickupStop:
        TNT1 A 0
        stop

      PickupFail:
        TNT1 A 0
        fail
        
      Nope:
        TNT1 A 1
        stop
    
    }
}

actor MegaPack : CustomInventory replaces Megasphere// replaces Megasphere
{
    -NOGRAVITY
    +INVENTORY.PICKUPFLASH
    +INVENTORY.BIGPOWERUP
    +INVENTORY.FANCYPICKUPSOUND
    Scale 0.50
    Inventory.PickupAnnouncerEntry "megasphere"
    inventory.pickupmessage "$SAMSARA_MEGAPACK"
    inventory.pickupsound "nosoundforyoubitch"
    States
    {
      Spawn:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,32,0,0) == 1, "Nope")
        FELD ABCDCB 4 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupCorvus")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "PickupMarathon")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupDuke")
        TNT1 A 0 A_JumpIfInventory("QuakeClass", 1, "PickupQuake")
      Pickup2:
        TNT1 A 0 A_PlaySound("megapackpickup", CHAN_ITEM)
        TNT1 A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 20, ARMOR_MEGASPHERE)
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth200", 200)
        stop
      
      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 200)
        stop

      PickupMarathon:
        TNT1 A 0 A_SetBlend("F0F8FF", 0.7, 10) // Light Alice Blue
        TNT1 A 0 A_PlaySound("megapackpickup", CHAN_ITEM)
        TNT1 A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 20, ARMOR_MEGASPHERE, -300)
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "InfiniteMarathon")
        TNT1 A 0 A_GiveInventory("FiniteHealth300", 300)
        stop

      InfiniteMarathon:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 300)
        stop

      PickupCorvus:
        TNT1 A 0 A_JumpIfInventory("CorvusMegasphere", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("CorvusMegasphere")
        stop

      PickupQuake:
        TNT1 A 0 A_PlaySound("quakeweps/megasphere", CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("QuakeRegenGiver")
        TNT1 A 0 A_GiveInventory("QuakeRedArmor")
        stop

      PickupFail:
        FELD A 1 BRIGHT
        fail
        
      PickupDuke:
        TNT1 A 0 A_JumpIfHealthLower(25, 1)
        goto Pickup2
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("DukeTauntCooldown", 1, "Pickup2")
        TNT1 A 0 A_JumpIfInventory("DukeBallgag", 1, "Pickup2")
        TNT1 A 0 A_PlaySoundEx("duke/criticalheal", "soundslot7", 0, 0)
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown", 5)
        TNT1 A 0 ACS_ExecuteAlways(205)
        goto Pickup2
        
      Nope:
        TNT1 A 1
        stop
    }
}

actor MarathonMegaHealth: Health
{
    Inventory.Amount 300
    Inventory.MaxAmount 300
}

actor PortQuartzFlask : HealthPickup
{
    //tag "Quartz Flask"
    health 25
    inventory.maxamount 16
    inventory.icon "ARTIQRTZ"
    inventory.usesound "heretic/itemuse"
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
}

actor PortQuartzFlask2 : HealthPickup
{
    //tag "Quartz Flask"
    health 25
    inventory.maxamount 16
    inventory.icon "ARTIQRTZ"
    inventory.usesound "hexen/artiuse"
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
}


actor PortSuperHealth : HealthPickup
{
    //tag "Mystic Urn"
    health 100
    inventory.maxamount 16
    inventory.icon "ARTISPHL"
    inventory.usesound "heretic/itemuse"
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
    HealthPickup.Autouse 2
}
actor PortSuperHealth2 : HealthPickup
{
    //tag "Mystic Urn"
    health 100
    inventory.maxamount 16
    inventory.icon "ARTISPHL"
    inventory.usesound "hexen/artiuse"
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
    HealthPickup.Autouse 2
}

actor CorvusMegasphere : CustomInventory
{
    //tag "Unbound Megasphere"
    Inventory.Amount 1
    Inventory.MaxAmount 16
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIFELD"
    Inventory.PickupSound "heretic/itemget"
    Inventory.UseSound "megapackpickup"
    Inventory.PickupMessage "You picked up an Unbound Megasphere!"
    States
    {
      Spawn:
        FELD ABCDCB 4 bright
        loop

      Use:
        TNT1 A 1 A_SetBlend("White", 0.1, 2)
        TNT1 A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 20, ARMOR_MEGASPHERE, 100)
        TNT1 A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 21, 200)
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "Infinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth200", 100)
        stop

      Infinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth", 100)
        stop
    }
}

actor CorvusMegaHealth : Health
{
    Inventory.Amount 100
    Inventory.MaxAmount 200
}
