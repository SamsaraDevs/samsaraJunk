actor HealPack0 : HealthBonus replaces HealthBonus
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.25
    +Floatbob
    -Inventory.ALWAYSPICKUP
    -COUNTITEM
    Inventory.PickupMessage "Picked up 1 Health."
    Health.LowMessage 25, "Picked up 1 Health."
    Inventory.PickupSound "healpack0pickup"
    States
    {
      Spawn:
        MEDI A 0
        MEDI A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop
    }
}

actor HealPack0Trail
{
    Renderstyle Add
    Alpha 0.4
    Scale 0.2
    -Solid
    +Noclip
    +Nogravity
    +Nointeraction
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        MEDI A 2 A_FadeOut(0.125)
        Loop
    }
}

actor HealPack1 : StimPack replaces StimPack
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.5
    +Floatbob
    Inventory.PickupMessage "Picked up 10 Health."
    Health.LowMessage 25, "Picked up 10 Health."
    Inventory.PickupSound "healpack1pickup"
    states
    {
      Spawn:
        MED2 A 0
        MED2 A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop
    }
}

actor HealPack1Trail
{
    Renderstyle Add
    Alpha 0.4
    Scale 0.45
    -Solid
    +Noclip
    +Nogravity
    +Nointeraction
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        MED2 A 2 A_FadeOut(0.125)
        Loop
    }
}

actor HealPack2 : CustomInventory replaces Medikit
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.75
    +Floatbob
    Inventory.PickupMessage "Picked up 25 Health."
    Inventory.PickupSound "notasingledamnsoundexists"
    states
    {
      Spawn:
        MED3 A 0
        MED3 A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "PickupHexen")
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupHeretic")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "PickupMarathon")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupDuke")
        TNT1 A 0 A_JumpIfInventory("RuneProsperity", 1, "PickupProsperity")
        TNT1 A 1
        TNT1 A 0 A_JumpIfHealthLower(100, "PickUpSuccess")

      PickupFail:
        MED3 A 1 BRIGHT
        fail

      PickUpSuccess:
        TNT1 A 0 A_PlaySound("healpack2pickup")
        TNT1 A 1 A_GiveInventory("Medikit")
        stop
        
      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("PortQuartzFlask", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("PortQuartzFlask")
        stop
        
      PickupHexen:
        TNT1 A 0 A_JumpIfInventory("PortQuartzFlask2", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("PortQuartzFlask2")
        stop
        
      PickupDuke:
        TNT1 A 0 A_JumpIfInventory("RuneProsperity", 1, "PickupDukeProsperity")
        TNT1 A 0 A_JumpIfHealthLower(25, "PickupDuke2")
        TNT1 A 0 A_JumpIfHealthLower(100, "PickupSuccess")
        goto PickupFail

      PickupDuke2:
        TNT1 A 0 A_JumpIfInventory("DukeTauntCooldown", 1, "PickupSuccess")
        TNT1 A 0 A_JumpIfInventory("DukeBallgag", 1, "PickupSuccess")
        TNT1 A 0 A_PlaySoundEx("duke/criticalheal", "soundslot7", 0, 0)
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown", 5)
        TNT1 A 0 ACS_ExecuteAlways(205)
        goto PickupSuccess
        
      PickupMarathon:
        TNT1 A 1 A_JumpIfInventory("RuneProsperity", 1, "PickupMarathonProsperity")
        TNT1 A 0 A_JumpIfHealthLower(100, "PickupMarathonSuccess")
        MED3 A 1 BRIGHT
        fail

      PickUpMarathonSuccess:
        TNT1 A 0 A_SetBlend("Green", 0.3, 3)
        TNT1 A 0 A_PlaySound("healpack2pickup")//A_PlaySoundEx("healpack2pickup", "item", 0, 0)
        TNT1 A 1 A_GiveInventory("Medikit")
        stop
        
      PickupProsperity:
        TNT1 A 1 A_JumpIfHealthLower(250, "PickUpSuccess")
        MED3 A 1 BRIGHT
        fail
        
      PickupDukeProsperity:
        TNT1 A 0 A_JumpIfHealthLower(25, "PickupDuke2")
        TNT1 A 0 A_JumpIfHealthLower(250, "PickupSuccess")
        MED3 A 1 BRIGHT
        fail
        
      PickupMarathonProsperity:
        TNT1 A 0 A_JumpIfHealthLower(250, "PickupMarathonSuccess")
        MED3 A 1 BRIGHT
        fail
    }
}

actor HealPack2Trail
{
    Renderstyle Add
    Alpha 0.4
    Scale 0.7
    -Solid
    +Noclip
    +Nogravity
    +Nointeraction
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        MED3 A 2 A_FadeOut(0.125)
        Loop
    }
}

actor SuperHealth : CustomInventory replaces Soulsphere
{
    RenderStyle Add
    Alpha 0.95
    +Floatbob
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +COUNTITEM
    Inventory.PickupAnnouncerEntry "soulsphere"
    Inventory.PickupMessage "Picked up 100 Health."
    Inventory.PickupSound "nosoundforyoubitch"
    states
    {
      Spawn:
        SOUL A 0
        SOUL A -1 BRIGHT ACS_ExecuteAlways(212, 0, 4, 100, 90)
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("QuakeClass", 1, "PickupQuake")
        TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupDuke")
        TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "PickupHexen")
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupHeretic")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "PickupMarathon")
        goto PickupSuccess

      PickupSuccess:
        TNT1 A 0 A_PlaySound("healpack3pickup", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("SoulSphere")
        stop

      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("PortSuperHealth", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("PortSuperHealth")
        stop

      PickupHexen:
        TNT1 A 0 A_JumpIfInventory("PortSuperHealth2", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("hexen/artiget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("PortSuperHealth2")
        stop

      PickupDuke:
        TNT1 A 0 A_JumpIfHealthLower(25, "PickupDuke2")

      PickupDukeSuccess:
        TNT1 A 0 A_PlaySound("duke/atomhealth", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("HealthBonus", 50)
        stop

      PickupDuke2:
        TNT1 A 0 A_JumpIfInventory("DukeTauntCooldown", 1, "PickupDukeSuccess")
        TNT1 A 0 A_JumpIfInventory("DukeBallgag", 1, "PickupDukeSuccess")
        TNT1 A 0 A_PlaySoundEx("duke/criticalheal", "soundslot7", 0, 0)
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown", 5)
        TNT1 A 0 ACS_ExecuteAlways(205)
        goto PickupDukeSuccess

      PickupMarathon:
        TNT1 A 0 A_SetBlend("F0F8FF", 0.5, 7) // Light Alice Blue
        TNT1 A 0 A_PlaySound("marathon/bigitemget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("SoulSphere")
        stop

      PickupQuake:
        TNT1 A 0 A_PlaySound("quakeweps/megahealth", CHAN_ITEM)
        TNT1 A 0 ACS_ExecuteAlways(249, 0, 100, 1, 175)
        stop

      PickupFail:
        SOUL A 1 BRIGHT
        fail
    }
}

actor SuperHealthTrail
{
    Renderstyle Add
    Alpha 0.5
    Scale 0.95
    -Solid
    +Noclip
    +Nogravity
    +Nointeraction
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        SOUL A 2 A_FadeOut(0.125)
        Loop
    }
}

actor MegaPack : CustomInventory replaces Megasphere// replaces Megasphere
{
    -NOGRAVITY
    +INVENTORY.PICKUPFLASH
    +INVENTORY.BIGPOWERUP
    +INVENTORY.FANCYPICKUPSOUND
    Scale 0.50
    Inventory.PickupAnnouncerEntry "megasphere"
    inventory.pickupmessage "Picked up 200 Health and 200 Armor!"
    inventory.pickupsound "nosoundforyoubitch"
    States
    {
      Spawn:
        FELD ABCDCB 4 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupCorvus")
        TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "PickupMarathon")
        TNT1 A 0 A_JumpIfInventory("QuakeClass", 1, "PickupQuake")
        TNT1 A 0 A_PlaySound("megapackpickup", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("Megasphere")
        stop

      PickupMarathon:
        TNT1 A 0 A_SetBlend("F0F8FF", 0.7, 10) // Light Alice Blue
        TNT1 A 0 A_PlaySound("megapackpickup", CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("MarathonMegaHealth")
        TNT1 A 1 A_GiveInventory("MarathonMegaArmor")
        stop

      PickupCorvus:
        TNT1 A 0 A_JumpIfInventory("CorvusMegasphere", 0, "PickupFail")
        TNT1 A 0 A_PlaySound("heretic/itemget", CHAN_ITEM)
        TNT1 A 1 A_GiveInventory("CorvusMegasphere")
        stop

      PickupQuake:
        TNT1 A 0 A_PlaySound("quakeweps/megasphere", CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("QuakeRegenGiver")//ACS_ExecuteAlways(249, 0, 250, 2, 175)
        TNT1 A 0 A_GiveInventory("QuakeRedArmor")
        stop

      PickupFail:
        FELD A 1 BRIGHT
        fail
    }
}

actor MarathonMegaHealth: Health
{
    Inventory.Amount 300
    Inventory.MaxAmount 300
}

actor MarathonMegaArmor : BasicArmorPickup
{
    Armor.SaveAmount 300
    Armor.SavePercent 50
    Armor.MaxFullAbsorb 1
    Inventory.Icon "RMR2A0"

    states
    {
      Spawn:
        ACLO E 1
        Stop
    }
}

actor QuakeRedArmor: BasicArmorPickup
{
    Armor.SaveAmount 200
    Armor.SavePercent 80
    Inventory.Icon "RMR3A0"

    States
    {
      Spawn:
        TNT1 A -1
        stop
    }
}

actor PortQuartzFlask : HealthPickup
{
    //tag "Quartz Flask"
    health 25
    inventory.maxamount 16
    inventory.icon "ARTIQRTZ"
    inventory.usesound "heretic/itemuse"
    +inventory.undroppable
    +inventory.hubpower
}

actor PortQuartzFlask2 : HealthPickup
{
    //tag "Quartz Flask"
    health 25
    inventory.maxamount 16
    inventory.icon "ARTIQRTZ"
    inventory.usesound "hexen/artiuse"
    +inventory.undroppable
    +inventory.hubpower
}


actor PortSuperHealth : HealthPickup
{
    //tag "Mystic Urn"
    health 100
    inventory.maxamount 16
    inventory.icon "ARTISPHL"
    inventory.usesound "heretic/itemuse"
    +inventory.undroppable
    +inventory.hubpower
    HealthPickup.Autouse 2
}
actor PortSuperHealth2 : HealthPickup
{
    //tag "Mystic Urn"
    health 100
    inventory.maxamount 16
    inventory.icon "ARTISPHL"
    inventory.usesound "hexen/artiuse"
    +inventory.undroppable
    +inventory.hubpower
    HealthPickup.Autouse 2
}

actor CorvusMegasphere : CustomInventory
{
    //tag "Unbound Megasphere"
    Inventory.Amount 1
    Inventory.MaxAmount 16
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIFELD"
    Inventory.PickupSound "heretic/itemget"
    Inventory.UseSound "megapackpickup"
    Inventory.PickupMessage "You picked up an Unbound Megasphere!"
    States
    {
      Spawn:
        FELD ABCDCB 4 bright
        loop

      Use:
		TNT1 A 1 A_SetBlend("White", 0.1, 2)
        TNT1 A 1 A_GiveInventory("CorvusMegaHealth", 1)
        TNT1 A 1 A_GiveInventory("CorvusMegaArmor", 1)
        stop
    }
}

actor CorvusMegaHealth : Health
{
    Inventory.Amount 100
    Inventory.MaxAmount 200
}
