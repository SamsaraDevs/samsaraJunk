actor SamsaraBloodyHellBlood : Blood //replaces Blood - Interestingly, it can't replace Blood for it to function. Otherwise in online play, you never see it at all.
{
    Game Doom
    States
    {
    Spawn:
	    TNT1 A 0
        BLUD C 1
		BLUD C 7 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33)
        BLUD BA 8
        stop
    }
}


actor Switchable_FlyingBlood
{
    Game Doom
    Scale 0.75
    Health 1
    Radius 8
    Height 1
    mass 1
	+CORPSE
	-SOLID
	+CANNOTPUSH
	+DONTSPLASH
	+NOTELEPORT
	+NOBLOCKMAP
	+CLIENTSIDEONLY
    states
    {
    Spawn:
	    TNT1 A 1
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,30) == 2, "SpawnBrutal")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,30) == 1, "SpawnNashgore")
		TNT1 A 1 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,30) == -1, "SpawnHappy")
		stop
	SpawnNashgore:
	    TNT1 A 0
        FBLD AABBCCDD 2 A_SpawnItemEx("Nashgore_FlyingBloodTrail",0,0,0,0,0,0,0,129)
        loop
	SpawnBrutal:
	    TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("RedMistTrail",0,0,0,0,0,0,0,161)
		TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",0,0,0,0,0,0,0,161)
		TNT1 A 0 A_SpawnItemEx("BrutalFlyingBloodTrail2",0,0,0,random(-16,16)*0.25,random(-16,16)*0.25,random(8,12),random(0,359),129)
		TNT1 AAAAAA 0 A_SpawnItemEx("BrutalBloodSpatter",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(200,600)/100.00,random(-180,180),161)
    	TNT1 AAAAA 0 A_SpawnItemEx("BrutalChunkTinier",0,0,0,random(-250,250)/100.00,random(-250,250)/100.00,random(200,800)/100.00,random(-180,180),161,25)
	SpawnBrutal2:
	    TNT1 A 0
		BRSP AABBCCDD 2 A_SpawnItemEx("BrutalFlyingBloodTrail",0,0,0,0,0,0,0,129)
		loop
	SpawnHappy:
	    TNT1 A 0
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
		TNT1 A 0 A_ChangeFlag("DoomBounce",1)
		TNT1 A 0 A_SpawnItemEx("RainbowSparkles",0,0,0,0,0,0,0,32)
		TNT1 AA 0 A_SpawnItemEx("WhiteSpark",0,0,0,random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
		TNT1 AAA 0 A_SpawnItemEx("RainbowSpark",0,0,0,random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
		stop
    Crash:
	    TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,30) == 2, "CrashBrutal")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,30) == 1, "CrashNashgore")
		TNT1 A 1
		stop
	CrashNashgore:
	    TNT1 A 0
        TNT1 A 1 A_SpawnItem("NashGore_BloodSpot",0,0,0,129)
        stop
	CrashBrutal:
	    TNT1 A 0
        TNT1 A 1 A_SpawnItem("BrutalLittlePuddle",0,0,0,129)
		TNT1 AAAAAAA 0 A_SpawnItemEx("BrutalBloodSpatter",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(200,800)/100.00,random(-180,180),169)
		BRSP EFGHIJKL 2
        stop
    }
}

actor Switchable_FlyingBlood_Brown : Switchable_FlyingBlood { Translation "16:47=48:79", "16:47=48:79" }
actor Switchable_FlyingBlood_Tan : Switchable_FlyingBlood { Translation "16:47=128:143", "16:47=128:143" }
actor Switchable_FlyingBlood_Forest : Switchable_FlyingBlood { Translation "16:47=152:159", "16:47=152:159" }
actor Switchable_FlyingBlood_Orange : Switchable_FlyingBlood { Translation "16:47=208:223", "16:47=208:223" }
actor Switchable_FlyingBlood_White : Switchable_FlyingBlood { Translation "16:47=80:95", "16:47=80:95" }
actor Switchable_FlyingBlood_Grey : Switchable_FlyingBlood { Translation "16:47=96:111", "16:47=96:111" }
actor Switchable_FlyingBlood_Black : Switchable_FlyingBlood { Translation "16:47=5:8", "16:47=5:8" }
actor Switchable_FlyingBlood_Yellow : Switchable_FlyingBlood { Translation "16:47=160:167", "16:47=160:167" }
actor Switchable_FlyingBlood_Pink : Switchable_FlyingBlood { Translation "16:47=250:254", "16:47=250:254" }
actor Switchable_FlyingBlood_Green : Switchable_FlyingBlood { Translation "16:47=112:127", "176:191=112:127" }
actor Switchable_FlyingBlood_Blue : Switchable_FlyingBlood { Translation "16:31=200:207", "32:47=240:247", "176:183=200:207", "184:191=240:247" }

actor GenericGoreActor
{
    +CANNOTPUSH
    +DONTSPLASH
    +NOTELEPORT
    +NOBLOCKMAP
    +MOVEWITHSECTOR
    -NOGRAVITY
    -SOLID
    +CLIENTSIDEONLY
	+FORCEXYBILLBOARD
}