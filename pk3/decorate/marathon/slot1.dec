actor UsingSMG: Boolean {}

actor "KKV-7 SMG Flechette" : Weapon// replaces SuperShotgun
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOSMGSkin"
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Decal BulletChip
    Inventory.PickupMessage "You picked up a KKV-7 SMG Flechette!"
    Weapon.AmmoType		"SmgBullet"
    Weapon.AmmoGive		0
    Weapon.AmmoUse		1
    Scale 0.35
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
    Radius				12
    Height				12
    States
    {
      Spawn:
        FSMG A -1
        STOP

      Ready:
        HSMG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfNoAmmo("Reload")
        LOOP

      Deselect:
        TNT1 A 0 A_Lower
        HSMG A 1 A_Lower
        LOOP

      Select:
        //TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        
        TNT1 A 0 A_GiveInventory("UsingSMG",1)
		TNT1 A 0 A_JumpIfNoAmmo("SelectReload")
        goto Select2
		
	  SelectReload:
        TNT1 A 0 A_JumpIfInventory("SmgBullet",0,"Select2")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Select2
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("SmgBullet",1)
        loop

      Select2:
        TNT1 A 0 A_Raise
        HSMG A 1 A_Raise
        LOOP

      Fire:
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBullet",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBullet",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_PlayWeaponSound("marathon/smg/fire")
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light2
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light1
        HSMG A 1 Offset (0,31) Bright
        TNT1 A 0 A_Light0
        TNT1 A 0 A_Refire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "UnderwaterCoopFire")
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletOverwater",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletOverwater",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_PlayWeaponSound("marathon/smg/fire")
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light2
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light1
        HSMG A 1 Offset (0,31) Bright
        TNT1 A 0 A_Light0
        TNT1 A 0 A_Refire
        Goto Ready

      UnderwaterCoopFire:
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletCoop",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletCoop",random(-10,10)/10.0,1,0,0,0,random(-10,10)/10.0)
        TNT1 A 0 A_PlayWeaponSound("marathon/smg/fire")
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,0,0,0)
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light2
        HSMG B 1 Offset (0,27) Bright
        TNT1 A 0 A_Light1
        HSMG A 1 Offset (0,31) Bright
        TNT1 A 0 A_Light0
        TNT1 A 0 A_Refire
        Goto Ready

      DryFire:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        HSMG A 3 A_PlaySound("marathon/rifle/dryfire")
        Goto Ready
        TNT1 A 0

      Reload:
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        Goto Ready
        HSMG C 5
        HSMG C 6 A_PlayWeaponSound("marathon/smg/reload")
        HSMG D 11
        HSMG E 11
        goto Reload2
		
	  Reload2:
        TNT1 A 0 A_JumpIfInventory("SmgBullet",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Ready
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("SmgBullet",1)
        loop
    }
}

actor SmgBullet : Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 32
}

actor MarathonSMGBullet : FastProjectile
{
    Damage (random(6,9))
    Radius 8
    Height 8
    Speed 100
    Projectile
    Obituary "%k \cnshredded\c- %o \cnwith the KKV-7.\c-"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +THRUGHOST
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop

      Crash:
        TNT1 A 1 A_PlaySound("puff/ding")
        stop
    }
}

actor MarathonSMGBulletCoop : MarathonSMGBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(9,15))
    +THRUSPECIES
    Species "Player"
}

actor MarathonSMGBulletOverwater : MarathonSMGBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(8,10))
    +THRUSPECIES
    Species "Player"
}

actor FlechetteCasingSpawner
{
    Radius 1
    Height 1
    Speed 25
    PROJECTILE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death

      Death:
        TNT1 A 0 A_CustomMissile("FlechetteCasing",1,5,random(-80,-110),2,random(30,40))
        Stop
    }
}

actor FlechetteCasing
{ 
    Scale 0.25
    Radius 3
    Height 3
    Speed 7
    Mass 5
    BounceCount 1
    +CLIENTSIDEONLY
    +THRUACTORS +DROPOFF +MISSILE +NOTELEPORT +FLOORCLIP
    +FORCEXYBILLBOARD +DOOMBOUNCE
    States
    {
      Spawn:
        FLEC ABCDEF 1
        Loop

      Death:
        TNT1 A 1
        Stop
    }
}
