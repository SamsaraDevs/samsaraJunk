actor UsingAlienWeapon: Boolean {}
actor UsingAlienWeapon2: Boolean {}

actor UnknownResonator1: Ammo
{
    Inventory.Amount 1
    Inventory.MaxAmount 100
    +IGNORESKILL
}

actor UnknownResonator2: Ammo
{
    Inventory.Amount 1
    Inventory.MaxAmount 100
    +IGNORESKILL
}

actor AlienWeaponFired: Counter {}
actor AlienWeaponFiredP: Counter {}
actor AlienWeaponFiredS: Counter {}
actor AlienWeaponSecondarySide: Boolean {}

actor "Alien Weapon" : Weapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOM2AWSkin"
    Weapon.SelectionOrder 500
    Weapon.SlotNumber 8
    Inventory.PickupMessage "system error 0xfded"
    Weapon.AmmoType "UnknownResonator1"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 0

    Scale 0.35
    Radius 12
    Height 12

    +NOALERT
    +AMMO_OPTIONAL
	+UNDROPPABLE

    States
    {
      Spawn:
        AWPN A -1
        stop

      Deselect:
        TNT1 A 0 A_Lower
        AWP1 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG", 1)
        TNT1 A 0 A_TakeInventory("UsingPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingFists", 1)
        TNT1 A 0 A_TakeInventory("UsingShotguns", 1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol", 1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle", 1)
        TNT1 A 0 A_TakeInventory("UsingSpanker", 1)
        TNT1 A 0 A_TakeInventory("UsingToasty", 1)
        //TNT1 A 0 A_TakeInventory("UsingAlienWeapon", 1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2", 1)
        TNT1 A 0 A_TakeInventory("UsingWMC", 1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns", 1)
        
        TNT1 A 0 A_GiveInventory("UsingAlienWeapon", 1)
        TNT1 A 0 A_Raise
        AWP1 A 1 A_Raise
        Goto Select+1

      GetRidOfThatShit:
        TNT1 A 0 A_TakeInventory("Alien Weapon", 1)
        TNT1 A 0 A_Lower
        AWP1 A 1 A_Lower
        loop

      Ready:
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo", 1, "ReadyStart")
        goto GetRidOfThatShit

      ReadyStart:
        AWP1 A 0 A_TakeInventory("AlienWeaponFired", 1)
        AWP1 A 0 A_TakeInventory("AlienWeaponFiredP", 1)
        AWP1 A 0 A_TakeInventory("AlienWeaponFiredS", 1)
        goto ReadyShow

      ReadyShow:
        AWP1 A 0 A_JumpIfInventory("AlienWeaponFired", 4, "ReadyFired")
        AWP1 A 0 A_JumpIfInventory("AlienWeaponFired", 1, "ReadyFired2")
        AWP1 A 1 A_WeaponReady
        goto Ready

      ReadyFired:
        AWP1 B 1 bright A_WeaponReady(1)
        goto Ready

      ReadyFired2:
        AWP1 A 1 A_WeaponReady(1)
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "CheckAltfire")
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredP", 1, "CheckAltfire")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo", 1, 1)
        goto CheckAltfire

        TNT1 A 0 A_PlaySound("enforcer/shoot", CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFired", 1, 2)
        TNT1 A 0 A_GiveInventory("AlienWeaponFired", 7)
        TNT1 A 0 A_GiveInventory("AlienWeaponFiredP", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 B 0 A_FireCustomMissile("EnforcerShot", random(-1, 1), 0, 0, 0, 0, random(-1, 1))
        goto Fire2

      CoopFire:
        TNT1 B 0 A_FireCustomMissile("EnforcerShotCoop", random(-1, 1), 0, 0, 0, 0, random(-1, 1))
        Goto Fire2

      Fire2:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Jump(64, 1)
        goto CheckAltfire
        TNT1 A 0 A_TakeInventory("UnknownAmmo", 1)
        goto CheckAltfire

      CheckAltfire:
        AWP1 A 0 A_JumpIfInventory("SynthFireRight", 1, "AltFire")
        goto ReadyShow

      AltFire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "ReadyShow")
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredS", 1, "ReadyShow")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo", 1, 1)
        goto ReadyShow

        TNT1 A 0 A_PlaySound("enforcer/shoot", CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFired", 1, 2)
        TNT1 A 0 A_GiveInventory("AlienWeaponFired", 7)
        TNT1 A 0 A_GiveInventory("AlienWeaponFiredS", 7)
        TNT1 A 0 A_JumpIfInventory("AlienWeaponSecondarySide", 1, "AltFire1")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAltFire1")
        TNT1 B 0 A_FireCustomMissile("EnforcerShot", -22, 0, 0, 0, 0, random(-1, 1))
        TNT1 B 0 A_GiveInventory("AlienWeaponSecondarySide")
        goto AltFire2

      AltFire1:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAltFire2")
        TNT1 B 0 A_FireCustomMissile("EnforcerShot", 22, 0, 0, 0, 0, random(-1, 1))
        TNT1 B 0 A_TakeInventory("AlienWeaponSecondarySide")
        goto AltFire2

      CoopAltFire1:
        AWP1 B 0 Bright A_FireCustomMissile("EnforcerShotCoop", -22, 0)
        TNT1 B 0 A_GiveInventory("AlienWeaponSecondarySide")
        goto AltFire2

      CoopAltFire2:
        AWP1 B 0 Bright A_FireCustomMissile("EnforcerShotCoop", 22, 0)
        TNT1 B 0 A_TakeInventory("AlienWeaponSecondarySide")
        goto AltFire2

      AltFire2:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Jump(64, 1)
        goto ReadyShow
        TNT1 A 0 A_TakeInventory("UnknownAmmo", 1)
        Goto ReadyShow
    }
}

actor " Alien Weapon " : Weapon
{
    Radius 12
    Height 12

    Weapon.AmmoType "UnknownResonator2"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 0

    +UNDROPPABLE
    +AMMO_OPTIONAL

    Weapon.SelectionOrder 500
    Weapon.SlotNumber 8
    Weapon.PreferredSkin "SOM1AWSkin"

    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Inventory.PickupMessage "system error 0xfded"
    Scale 0.35

    States
    {
      Spawn:
        AWPN B -1
        STOP

      GetRidOfThatShit:
        TNT1 A 0 A_TakeInventory(" Alien Weapon ", 1)
        TNT1 A 0 A_Lower
        AWP1 A 1 A_Lower
        loop

      Ready:
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo2", 1, 1)
        goto GetRidOfThatShit

      Ready2:
        AWP2 A 1 A_WeaponReady
        Loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG", 1)
        TNT1 A 0 A_TakeInventory("UsingPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingFists", 1)
        TNT1 A 0 A_TakeInventory("UsingShotguns", 1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol", 1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle", 1)
        TNT1 A 0 A_TakeInventory("UsingSpanker", 1)
        TNT1 A 0 A_TakeInventory("UsingToasty", 1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon", 1)
        //TNT1 A 0 A_TakeInventory("UsingAlienWeapon2", 1)
        TNT1 A 0 A_TakeInventory("UsingWMC", 1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns", 1)
        
        TNT1 A 0 A_GiveInventory("UsingAlienWeapon2", 1)
        AWP2 A 0 A_Raise
        AWP2 A 1 A_Raise
        Loop

      DeSelect:
        AWP2 A 0 A_Lower
        AWP2 A 1 A_Lower
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        AWP2 B 1 A_Recoil(0.1)
        TNT1 A 0 A_Light2
        TNT1 A 0 A_PlayWeaponSound("alienweapon/shoot")
        TNT1 A 0 A_FireCustomMissile("AlienWeaponBullet", random(-3, 3), 0, 0, 0)
        AWP2 C 2 bright A_FireCustomMissile("AlienWeaponBullet", random(-2, 2), 0, 0, 0)//A_FireBullets(7.5, 0, 3, 5, "alienpuff", 1, 0)
        TNT1 A 0 A_Light0
        AWP2 B 1
        TNT1 A 0 A_Jump(48, 1)
        goto Ready
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("UnknownAmmo2", 1)
        Goto Ready

      CoopFire:
        AWP2 B 1 A_Recoil(0.1)
        TNT1 A 0 A_Light2
        TNT1 A 0 A_PlayWeaponSound("alienweapon/shoot")
        TNT1 A 0 A_FireCustomMissile("AlienWeaponBulletCoop", random(-3, 3), 0, 0, 0)
        AWP2 C 2 bright A_FireCustomMissile("AlienWeaponBulletCoop", random(-2, 2), 0, 0, 0)//A_FireBullets(7.5, 0, 3, 5, "alienpuff", 1, 0)
        TNT1 A 0 A_Light0
        AWP2 B 1
        TNT1 A 0 A_Jump(48, 1)
        goto Ready
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("UnknownAmmo2", 1)
        Goto Ready
    }
}

actor UnknownAmmo : Ammo 
{
    +IGNORESKILL
    Inventory.MaxAmount 50
}

actor UnknownAmmo2 : Ammo 
{
    +IGNORESKILL
    Inventory.MaxAmount 50
}

actor EnforcerShot
{
    Decal Scorch
    Radius 6
    Height 8
    Speed 15
    Damage (random(30, 38)) // Formerly 20, 28
    DamageType Fire
    -BLOODSPLATTER
    +NOBLOOD
    +BLOODLESSIMPACT
    Scale 0.35
    DeathSound "enforcer/hit"
    Obituary "%k \cnkildldedd\c- %o \cnwitfji09 tth e   44 \c[c2]SYSTEM ERROR\c-"
    Projectile
    States
    {
      Spawn:
        PLAS ABCDEF 2 Bright
        LOOP

      Death:
        PHIT ABCD 2 Bright
        STOP
    }
}

actor AlienWeaponBullet : FastProjectile
{
    Damage (random(10, 15)) // Formerly 10-18
    Radius 1
    Height 1
    Speed 80
    Projectile
    DamageType MarathonPlasma
    Obituary "%k \cnkildldedd\c- %o \cnwitfji09 tth e   44 \c[c2]SYSTEM ERROR\c-"
    -BLOODSPLATTER
    +NOBLOOD
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +BLOODLESSIMPACT
    Decal RailScorchLower
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("MarathonAlienPuff")
        stop
    }
}

actor MarathonAlienPuff
{
    scale .375
    +PUFFONACTORS
    +FORCEXYBILLBOARD
    +NOBLOCKMAP
    +NOGRAVITY
    +NOEXTREMEDEATH
    +FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    VSpeed .01
    Mass 5
    States
    {
      Spawn:
        TNT1 A 0 A_PlaySound("alienweapon/hit")
        APUF ABCDE 2 bright
        stop

      Crash:
        TNT1 A 0 A_PlaySound("alienweapon/hit")
        APUF ABCDE 2 bright
        stop

      Death:
        TNT1 A 0 A_PlaySound("alienweapon/hit")
        APUF ABCDE 2 bright
        stop
    }
}

actor AlienWeaponBulletCoop : AlienWeaponBullet
{
    +THRUSPECIES
    Species "Player"
}

actor EnforcerShotCoop : EnforcerShot
{
    +THRUSPECIES
    Species "Player"
}


