actor UsingAssaultRifle : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 }

actor AssaultFired: Counter {}
actor AssaultFiredP: Counter {}
actor AssaultFiredS: Counter {}

actor "MA-75B Assault Rifle" : Weapon// replaces Chaingun
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.SelectionOrder 100
    Weapon.SlotNumber 4
    Decal BulletChip
    Inventory.PickupMessage "You picked up a MA-75B Assault Rifle!"
    Weapon.AmmoType		"RifleBullet"
    Weapon.AmmoGive		100
    Weapon.AmmoUse		1
    Weapon.AmmoType2	"GrenadeInClip"
    Weapon.AmmoGive2	100
    Weapon.AmmoUse2		1
    Scale 0.35
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +NOAUTOAIM
    +NOALERT
    +INVENTORY.UNDROPPABLE
    Radius				12
    Height				12
    States
    {
      Spawn:
        RIFL A -1
        STOP
        
      Deselect:
        TNT1 A 0 A_Lower
        ASLT A 1 A_Lower
        Loop
        
      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG", 1)
        TNT1 A 0 A_TakeInventory("UsingPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingFists", 1)
        TNT1 A 0 A_TakeInventory("UsingShotguns", 1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol", 1)
        //TNT1 A 0 A_TakeInventory("UsingAssaultRifle", 1)
        TNT1 A 0 A_TakeInventory("UsingSpanker", 1)
        TNT1 A 0 A_TakeInventory("UsingToasty", 1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon", 1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2", 1)
        TNT1 A 0 A_TakeInventory("UsingWMC", 1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols", 1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns", 1)
        
        TNT1 A 0 A_GiveInventory("UsingAssaultRifle", 1)
        goto Select2

      Select2:
        TNT1 A 0 A_Raise
        ASLT A 1 A_Raise
        loop

      Ready:
        ASLT A 0 A_TakeInventory("AssaultFired", 1)
        ASLT A 0 A_TakeInventory("AssaultFiredS", 1)
        ASLT A 0 A_TakeInventory("AssaultFiredP", 1)
        goto ReadyShow
        
      ReadyShow:
        ASLT A 0 A_JumpIfInventory("AssaultFiredP", 3, "ReadyFired")
        ASLT A 0 A_JumpIfInventory("AssaultFiredP", 2, "ReadyFired2")
        ASLT A 0 A_JumpIfInventory("AssaultFiredP", 1, "ReadyBright")
        ASLT A 1 A_WeaponReady
        goto Ready

      ReadyFired:
        ASLT C 1 bright A_WeaponReady(5)
        goto Ready

      ReadyFired2:
        ASLT B 1 bright A_WeaponReady(5)
        goto Ready

      ReadyBright:
        ASLT A 1 bright A_WeaponReady(5)
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "DryFire")
        TNT1 A 0 A_JumpIfInventory("AssaultFiredP", 1, "CheckAlt")
        TNT1 A 0 A_JumpIfNoAmmo("CheckReload")

        TNT1 A 0 A_GiveInventory("AssaultFiredP", 3)

        TNT1 A 0 A_PlaySound("marathon/rifle/fire", CHAN_WEAPON)
        TNT1 A 0 A_FireCustomMissile("32mmCasingSpawner", 0, 0, 0, -13)

        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireCustomMissile("MarathonARBullet", random(-4, 4), 1, 0, 0, 0, random(-4, 4))
        goto Fire2

      CoopFire:
        TNT1 A 0 A_FireCustomMissile("MarathonARBulletCoop", random(-5, 5), 1, 0, 0, 0, random(-5, 5))//A_FireBullets(10, 10, -1, 9+random(0, 6), "MarathonPuff")
        goto Fire2

      Fire2:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        goto CheckAlt

      CheckAlt:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight", 1, "AltFire")
        goto ReadyShow

      CheckReload:
        TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadP")
        TNT1 A 0 A_CheckReload  // almost guaranteed to switch
        goto CheckAlt

      ReloadP:
        ASLT D 10 A_PlaySound("marathon/rifle/reload", CHAN_WEAPON)
        ASLT E 11
        goto ReloadP2

      ReloadP2:
        TNT1 A 0 A_TakeInventory("Clip", 1)
        TNT1 A 0 A_GiveInventory("RifleBullet", 1)
        TNT1 A 0 A_JumpIfInventory("RifleBullet", 0, "ReloadP3")
        TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadP2")
        goto ReloadP3

      ReloadP3:
        ASLT F 10
        goto CheckAlt
     
      AltFire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "DryFire")
        TNT1 A 0 A_JumpIfInventory("AssaultFiredS", 1, "ReadyShow")
        TNT1 A 0 A_JumpIfInventory("GrenadeInClip", 1, 1)
        goto CheckAltReload

        TNT1 A 0 A_GiveInventory("AssaultFiredS", 16)
        TNT1 A 0 A_TakeInventory("GrenadeInClip", 1) // aannndd there goes sv_infiniteammo
        TNT1 A 0 A_PlaySound("marathon/rifle/grenade", 6)

        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopAltFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAltFire")
        TNT1 A 0 A_FireCustomMissile("MarathonGrenade", 0, 0, 0, 6, 0, 4)
        goto Fire2

      CoopAltFire:
        TNT1 A 0 A_FireCustomMissile("MarathonGrenadeCoop", 0, 0, 0, 6, 0, 4)
        goto Fire2

      CheckAltReload:
        TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, "AltReloadP")
        TNT1 A 0 A_CheckReload
        goto ReadyShow

      AltReloadP:
        ASLT A 14 A_PlaySound("marathon/rifle/reload", CHAN_WEAPON)
        goto AltReloadP2

      AltReloadP2:
        TNT1 A 0 A_TakeInventory("RocketAmmo", 1)
        TNT1 A 0 A_GiveInventory("GrenadeInClip", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeInClip", 0, "AltReloadP3")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, "AltReloadP2")
        goto AltReloadP3

      AltReloadP3:
        ASLT A 1
        goto ReadyShow
      
      DryFire:
        ASLT A 3 A_PlaySound("marathon/rifle/dryfire", CHAN_AUTO)
        goto Ready
    }
        


    /* Old shit
    {
      Ready:
        ASLT A 1 A_WeaponReady
        TNT1 A 0 A_GiveInventory("DukeFlying", 1) // This is SUPER-HACKY.
        loop
        
      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "NothingFire")
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("MarathonARBullet", random(-4, 4), 1, 0, 0, 0, random(-4, 4))//A_FireBullets(10, 10, -1, 9+random(0, 6), "MarathonPuff")
        TNT1 A 0 A_PlaySound("marathon/rifle/fire")
        TNT1 A 0 A_FireCustomMissile("32mmCasingSpawner", 0, 0, 0, -13)
        TNT1 A 0 A_GunFLash
        ASLT B 1 Bright A_WeaponReady(4)
        ASLT A 1 Bright A_WeaponReady(4)
        Goto Ready
        
      CoopFire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("MarathonARBulletCoop", random(-5, 5), 1, 0, 0, 0, random(-5, 5))//A_FireBullets(10, 10, -1, 9+random(0, 6), "MarathonPuff")
        TNT1 A 0 A_PlaySound("marathon/rifle/fire")
        TNT1 A 0 A_FireCustomMissile("32mmCasingSpawner", 0, 0, 0, -13)
        TNT1 A 0 A_GunFLash
        ASLT B 1 Bright A_WeaponReady(4)
        ASLT A 1 Bright A_WeaponReady(4)
        Goto Ready
        
      NothingFire:
        TNT1 A 0
        goto Ready
        
      DryFire:
        TNT1 A 0 A_JumpIfInventory("Clip", 1, 2)
        ASLT A 3 A_PlaySound("marathon/rifle/dryfire")
        Goto Ready
        TNT1 A 0
        Goto LReload
        
      LReload:
        TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
        Goto Ready
        ASLT D 10 A_PlaySoundEx("marathon/rifle/reload", "Auto", 0)
        ASLT E 11
        ASLT F 10
        
      Reload2:
        TNT1 A 0 A_TakeInventory("Clip", 1)
        TNT1 A 0 A_GiveInventory("RifleBullet", 1)
        TNT1 A 0 A_JumpIfInventory("RifleBullet", 0, 3)
        TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
        Goto Ready
        TNT1 A 0
        Goto Reload2
        TNT1 A 0
        ASLT A 1
        Goto Ready
        
      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 1 A_Light1
        TNT1 A 1 A_Light0
        stop
        
      AltFire:
        TNT1 A 0 A_GiveInventory("DukeFlying", 1) // More hackery.
        TNT1 A 0 A_JumpIf(waterlevel == 3, "NothingFire")
        TNT1 A 0 A_JumpIfInventory("GrenadeInClip", 1, "AltFire2") // Hack, hack, hack.
        TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, "RReload")
        ASLT A 0 A_PlaySoundEx("marathon/rifle/dryfire", "Auto", 0)
        ASLT A 35 A_WeaponReady(8)
        Goto Ready
        
      AltFire2:
        TNT1 A 0 A_JumpIfInventory("DukeJetpackFuel", 1, "NothingFire2")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopAltFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAltFire")
        TNT1 A 0 A_Recoil(3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("MarathonGrenade", 0, 1, 0, 6, 0, 4)
        TNT1 A 0 A_PlaySoundEx("marathon/rifle/grenade", "Auto", 0)
        TNT1 A 0 A_GiveInventory("DukeJetpackFuel", 1) // The shit I do for folks.
        TNT1 A 0 ACS_ExecuteAlways(214, 0, 1)
        ASLT A 1 A_Light0
        //Goto Ready
      RReload:
        TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, 1)
        Goto Ready
        ASLT A 15 A_PlaySoundEx("marathon/rifle/reload", "Auto", 0)
        
      Reload3:
        TNT1 A 0 A_TakeInventory("RocketAmmo", 1)
        TNT1 A 0 A_GiveInventory("GrenadeInClip", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeInClip", 0, 3)
        TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, 1)
        Goto Ready
        TNT1 A 0
        Goto Reload3
        TNT1 A 0 
        ASLT A 1
        Goto Ready
        
      CoopAltFire:
        TNT1 A 0 A_Recoil(3)
        TNT1 A 0 A_AlertMonsters//, 0, 0, 0, 0)
        TNT1 A 0 A_FireCustomMissile("MarathonGrenadeCoop", 0, 1, 0, 6, 0, 4)
        TNT1 A 0 A_PlaySoundEx("marathon/rifle/grenade", "Auto", 0)
        TNT1 A 0 A_GiveInventory("DukeJetpackFuel", 1) // haacx
        TNT1 A 0 ACS_ExecuteAlways(214, 0, 1)
        //ASLT A 10 A_Light2
        //ASLT A 10 A_Light1
        ASLT A 1 A_Light0
        TNT1 A 0 A_JumpIfInventory("GrenadeInClip", 1, "Ready")
        goto RReload
        
      NothingFire2:
        ASLT A 6
        goto Ready
    }
    */
}

actor MarathonARBullet : FastProjectile
{
    Damage (random(9, 15))
    Radius 8
    Height 8
    Speed 100
    Projectile
    Obituary "%k \cnassaulted\c- %o \cnwith the MA-75B.\c-"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +THRUGHOST
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop
        
      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop
        
      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop
        
      Crash:
        TNT1 A 1 A_PlaySound("puff/ding")
        stop
    }
}

actor MarathonARBulletCoop : MarathonARBullet
{
    Radius 1
    Height 1
    Speed 80
    +THRUSPECIES
    Species "Player"
}

//*****Assault Rifle*****
actor 32mmCasingSpawner
{
    Radius 1
    Height 1
    Speed 25
    PROJECTILE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death
        
      Death:
        TNT1 A 0 A_CustomMissile("32mmCasing", 1, 5, random(-80, -110), 2, random(30, 40))
        Stop
    }
}

actor 32mmCasing
{ 
    Scale 0.25
    Radius 3
    Height 3
    Speed 7
    Mass 5
    BounceCount 1
    +CLIENTSIDEONLY
    +THRUACTORS +DROPOFF +MISSILE +NOTELEPORT +FLOORCLIP
    +FORCEXYBILLBOARD +DOOMBOUNCE
    States
    {
      Spawn:
        RBLT ABCDEF 1
        Loop
        
      Death:
        TNT1 A 1
        //RBLT EEEEEEEEEEEEEEEEEEEE 1 A_FadeOut(0.05)
        Stop
    }
}

actor RifleBullet : Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 52
}

actor GrenadeInClip : Ammo // In this case it actually IS a clip!
{
    +IGNORESKILL
    Inventory.MaxAmount 7
}

actor MarathonGrenade
{
    Decal Scorch
    Radius 6
    Height 8
    Mass 1
    Speed 17
    Damage 4
    DamageType "Explosive"
    Scale 0.55
    //Gravity 0.11
    DeathSound "marathon/rifle/nadexpl"
    Projectile
    Obituary "%k \cnwent with\c- %o \cnin a game of catch-the-grenade.\c-"
    -NOGRAVITY
    +LOWGRAVITY
    +FORCERADIUSDMG
    //+GRENADETRAIL
    States
    {
      Spawn:
        NADE AAAAAAAAAAAAAAA 1 Bright A_SpawnItem("MarathonGrenadeSmoke")
        
      Spawn2:
        NADE A 1 Bright
        LOOP
        
      Death:
        TNT1 A 0 A_NoGravity
        XPLD A 4 Bright A_Explode(70+random(0, 20), 128)
        XPLD B 4 Bright 
        XPLD C 4 Bright
        XPLD D 4 Bright
        XPLD E 4 Bright
        XPLD F 4 Bright
        Stop
    }
}

actor MarathonGrenadeCoop : MarathonGrenade
{
    Speed 15
    +THRUSPECIES
    Species "Player"
    States
    {
      Death:
        TNT1 A 0 A_NoGravity
        XPLD A 4 Bright A_Explode(80+random(0, 20), 128)
        XPLD B 4 Bright 
        XPLD C 4 Bright
        XPLD D 4 Bright
        XPLD E 4 Bright
        XPLD F 4 Bright
        Stop
    }
}

actor MarathonGrenadeSmoke
{
    Radius 1
    Height 1
    Speed 0
    +NOGRAVITY
    -SOLID
    +NOCLIP
    +RANDOMIZE
    +CLIENTSIDEONLY
    Scale 0.15
    States
    {
      Spawn:
        TNT1 A 2
        FLME CD 1 BRIGHT
        MSMK ABCD 1
        stop
    }
}
