actor UsingFists: Boolean {}
actor DOUBLEFISTING: Boolean {}
actor GibfistSide: Boolean {}

actor KnifeAmmo : Ammo // You need ammo to stab people. Makes sense.
{
    +IGNORESKILL
    Inventory.MaxAmount 2
}

actor "Talon Ceramic Combat Blade" : Weapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOFistSkin"
    Weapon.SelectionOrder 3700
    Obituary "%k \cndug into\c- %o\cn's spine.\c-"
    Weapon.AmmoType		"KnifeAmmo"
    Weapon.AmmoGive		1
    Weapon.AmmoUse		0
    Weapon.Kickback 300
    +MELEEWEAPON
    +NOALERT
    +NOEXTREMEDEATH
    +INVENTORY.UNDROPPABLE
    +CHEATNOTWEAPON
    States
    {
      Deselect:
        TNT1 A 0 A_Lower
        MRNF A 1 A_Lower
        LOOP
        
      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        //TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        
        TNT1 A 0 A_GiveInventory("UsingFists",1)
        TNT1 A 0 A_Raise
        MRNF A 1 A_Raise
        loop
        
      Ready:
        MRNF A 1 A_WeaponReady
        loop
        
      Fire:
        MRNF BCD 2
        MRNF DE 1
        MRNF F 1 A_CustomPunch(random(36,40),1,0,"MarathonKnifePuff",96)
        MRNF G 1 A_CustomPunch(random(36,40),1,0,"MarathonKnifePuff",96)
        MRNF H 1 A_CustomPunch(random(36,40),1,0,"MarathonKnifePuff",96)
        TNT1 A 10
        TNT1 AAAAAAAAAAA 0 A_Lower
        Goto Select
        
      AltFire:
        MRNT ABC 2
        TNT1 A 0 A_PlayWeaponSound("marathon/knife/throw")
        MRNT CDE 1
        MRNT F 1 A_FireCustomMissile("TCCBThrown",0,0,0,2)//(random(100,120),1,0,"MarathonKnifePuff",96)
		TNT1 A 0 A_JumpIfInventory("KnifeAmmo",2,"TakeAmmo")
        MRNT GHI 1
		TNT1 A 5 A_TakeInventory("Talon Ceramic Combat Blade")
        TNT1 A 5 A_SelectWeapon("Steel Knuckles")
        TNT1 AAAAAAAAAAA 0 A_Lower
        Goto Select
		
	  TakeAmmo:
	    TNT1 A 0 A_TakeInventory("KnifeAmmo",1)
        MRNT GHI 1 
        TNT1 A 10
        TNT1 AAAAAAAAAAA 0 A_Lower
        Goto Select
    }
}

ACTOR TCCBThrown
{
    PROJECTILE
    +NOEXTREMEDEATH
    +RIPPER
    +DOOMBOUNCE
    +NOGRAVITY
    Speed 50
    Height 12
    Radius 12
    Damage (random(30,40))
    XScale 0.5
    YScale 0.25
    BounceCount 12
    BounceFactor 1.0
    WallBounceFactor 1.0
    DeathSound "marathon/knife/wall"
    SeeSound "marathon/knife/bounce"
	Obituary "%k \cnput\c- %o \cnin the blender.\c-"
    DropItem TCCBPickup
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("silence","voice",0,0)

      Spawn2:
        MRNP ABCD 1
        loop

      Death:
        MRNF Z 1
        TNT1 A 0 A_NoBlocking
        Stop

      Crash:
        MRNF Z 1
        TNT1 A 0 A_NoBlocking
        Stop
    }
}

actor TCCBPickup : CustomInventory
{
    Inventory.PickupMessage "You picked up the Talon Ceramic Combat Blade!"
    Inventory.PickupSound "marathon/itemget"
    -INVENTORY.ALWAYSPICKUP
    -COUNTITEM
    States
    {
      Spawn:
        MRNF Z 5 BRIGHT
        MRNF Z 15
        loop
        
      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupCheck")
        goto PickupFail
        
      PickupCheck:
        TNT1 A 0 A_JumpIfInventory("Talon Ceramic Combat Blade",1,"PickupCheck2")
        TNT1 A 0 A_SetBlend("Green", 0.4, 5)
        TNT1 A 1 A_GiveInventory("Talon Ceramic Combat Blade",1)
		TNT1 A 0 A_JumpIfInventory("KnifeAmmo",2,"GrossHack")
        stop
		
	  PickupCheck2:
	    TNT1 A 0 A_JumpIfInventory("KnifeAmmo",2,"PickupFail")
        TNT1 A 0 A_SetBlend("Green", 0.4, 5)
        TNT1 A 1 A_GiveInventory("KnifeAmmo",1)
        stop
		
	  GrossHack:
	    TNT1 A 0 A_TakeInventory("KnifeAmmo",1)
		stop
        
      PickupFail:
        MRNF Z 1
        fail
    }
}

actor "Tech.50 Pacifier" : Weapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOFusionSkin"
    Weapon.SelectionOrder 4000
    Inventory.PickupMessage "You picked up a Tech.50 Pacifier Security Handgun!"
    Scale 0.35
    +NOALERT
    +INVENTORY.UNDROPPABLE
    States
    {
      Spawn:
        FUSP A -1
        STOP

      Ready:
        MRTK A 1 A_WeaponReady
		loop
		
	  Select:
        MRTK A 1 A_Raise
        loop
		
	  Deselect:
        MRTK A 1 A_Lower
        loop

      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "WaterFire1")
        MRTK A 1
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("Tech50Shot",1,0,5,6,0,0)
        TNT1 A 0 A_FireCustomMissile("Tech50Shot",-1,0,-5,6,0,0)
        TNT1 A 0 A_PlaySound("marathon/tech50/fire")
        MRTK A 2 A_Light2
        MRTK B 2 Bright A_Light1 
        MRTK D 2 A_Light0
        MRTK C 3
        MRTK A 10
        Goto Reload

      WaterFire1:
        MRTK A 1
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("Tech50ShotUnderwater",1,0,5,6,0,0)
        TNT1 A 0 A_FireCustomMissile("Tech50ShotUnderwater",-1,0,-5,6,0,0)
        TNT1 A 0 A_PlaySound("marathon/tech50/fire")
        MRTK A 2 A_Light2
        MRTK B 2 Bright A_Light1 
        MRTK D 2 A_Light0
        MRTK C 3
        MRTK A 10
		
	  Reload:
        MRTK A 0 Offset(0,32)
        MRTK A 1 Offset(0,35)
        MRTK A 1 Offset(0,40)
        MRTK A 1 Offset(0,45)
        MRTK A 1 Offset(0,50)
        MRTK A 1 Offset(0,55)
        MRTK A 1 Offset(0,60)
        MRTK A 1 Offset(0,65)
        MRTK A 1 Offset(0,70)
        MRTK A 1 Offset(0,75)
        MRTK A 1 Offset(0,80) // 10
        MRTK A 1 Offset(0,85)
        MRTK A 1 Offset(0,90)
        MRTK A 1 Offset(0,95)
        MRTK A 1 Offset(0,100)
        MRTK A 1 Offset(0,105)
        MRTK A 1 Offset(0,110)
        MRTK A 1 Offset(0,115)
        MRTK A 1 Offset(0,120)
        MRTK A 1 Offset(0,125)
        MRTK A 1 Offset(0,130) // 20
        MRTK A 1 Offset(0,135)
        MRTK A 1 Offset(0,140)
        MRTK A 10
		goto Select
	}
}

actor Tech50Shot
{
    Decal DoomImpScorch
    Radius 8
    Height 10
    Speed 50
    Damage (5)
	RenderStyle Add
    DamageType "MarathonStun"
    Scale 0.075
    //DeathSound "marathon/tech50/stun"
    +FOILINVUL
    +PIERCEARMOR
    +BLOODLESSIMPACT
    Obituary "%k \cnbrought\c- %o \cnin for resisting security.\c-"
    Projectile
    States
    {
      Spawn:
		TNT1 A 1
	    MRTK E 1 BRIGHT
		TNT1 A 1
		MRTK F 1 BRIGHT
		TNT1 A 1
		MRTK G 1 BRIGHT
        stop

      Death:
        TNT1 A 1
		stop
    }
}

actor Tech50ShotUnderwater : Tech50Shot
{
    States
    {
      Spawn:
	    DPLE B 1
      Death:
        TNT1 A 1 A_Explode(64,96)
		stop
    }
}

actor NothingSpeed : PowerSpeed
{
  Powerup.Duration -3
  Speed 0.1
}

actor "Steel Knuckles" : Weapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOFistSkin"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Obituary "%k \cnsnapped\c- %o \cnwith a nasty hook.\c-"
    //AttackSound "marathon/fist/hit"
    Weapon.Kickback 300
    +WIMPY_WEAPON
    +MELEEWEAPON
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +NOBLOOD
    States
    {
      Deselect:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"Deselect2")
        TNT1 A 0 A_TakeInventory("DOUBLEFISTING",1)
        TNT1 A 0 A_Lower
        FIS2 A 1 A_Lower
        LOOP
        
      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        //TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        
        TNT1 A 0 A_GiveInventory("UsingFists",1)
        TNT1 A 0 A_Raise
        FIS2 A 1 A_Raise
        loop
        
      Ready:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"Ready2")
        FIS2 A 1 A_WeaponReady
        loop
        
      Deselect2:
        TNT1 A 0 A_TakeInventory("DOUBLEFISTING",1)
        TNT1 A 0 A_Lower
        TNT1 A 0 A_TakeInventory("DOUBLEFISTING",1)
        FIS3 A 1 A_Lower
        LOOP
        
      Ready2:
        FIS3 A 1 A_WeaponReady
        loop
        
      Fire:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"FistCheck")
        TNT1 A 0 A_JumpIf((momx*momx) + (momy*momy) >= 49,"Fire2")
        FIS2 B 3
        FIS2 C 4 A_CustomPunch(random(12,14),1,0,"BlankPuffMarathon",96)
        FIS2 B 3
        FIS2 A 7
        Goto Ready
        
      Fire2:
        FIS2 B 3
        FIS2 C 4 A_CustomPunch(random(60,70),1,0,"BlankPuffMarathon",96) // Formerly 50-60
        FIS2 B 3
        FIS2 A 7
        Goto Ready
        
      FistCheck:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight", 1, 1)
        goto LeftFist1
        TNT1 A 0 A_JumpIfInventory("GibfistSide", 1, "RightFist1")
        goto LeftFist1
        
      LeftFist1:
        FIS4 A 0 A_GiveInventory("GibfistSide")
        TNT1 A 0 A_JumpIf(momx >= 6,"LeftFist2")
        TNT1 A 0 A_JumpIf(momx <= -6,"LeftFist2")
        FIS4 ABC 1
        FIS4 D 1 A_CustomPunch(random(12,14),1,0,"BlankPuffMarathon",96)
        FIS4 E 2
        FIS4 D 1
        FIS4 CBA 1
        FIS3 A 7
        Goto Ready
        
      LeftFist2:
        FIS4 ABC 1
        FIS4 D 1 A_CustomPunch(random(60,70),1,0,"BlankPuffMarathon",96) // Formerly 50-60
        FIS4 E 2
        FIS4 D 1
        FIS4 CBA 1
        FIS3 A 7
        Goto Ready
        
      AltFire:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"RightFist1")
        FIS3 KIGEDCBA 1//LKJIHGFEDDCCBBAA 1
        TNT1 A 0 A_GiveInventory("DOUBLEFISTING",1)
        goto Ready2
        
      AltFistCheck:
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft", 1, 1)
        goto RightFist1
        TNT1 A 0 A_JumpIfInventory("GibfistSide", 1, "RightFist1")
        goto LeftFist1
        
      RightFist1:
        FIS5 A 0 A_TakeInventory("GibfistSide")
        TNT1 A 0 A_JumpIf(momx >= 6,"RightFist2")
        TNT1 A 0 A_JumpIf(momx <= -6,"RightFist2")
        FIS5 ABC 1
        FIS5 D 1 A_CustomPunch(random(12,14),1,0,"BlankPuffMarathon",96)
        FIS5 E 2
        FIS5 D 1
        FIS5 CBA 1
        FIS3 A 7
        Goto Ready
        
      RightFist2:
        FIS5 ABC 1
        FIS5 D 1 A_CustomPunch(random(60,70),1,0,"BlankPuffMarathon",96) // Formerly 50-60
        FIS5 E 2
        FIS5 D 1
        FIS5 CBA 1
        FIS3 A 7
        Goto Ready
        
    }
}

actor BlankPuffMarathon : BulletPuff
{
    +CLIENTSIDEONLY
    +PUFFONACTORS
    +NOBLOOD
    //ActiveSound "marathon/fist/hit"
    AttackSound "marathon/fist/hit"
    //SeeSound "marathon/fist/hit"
    DamageType "Pistol"
    States
    {
      Melee:
      Spawn:
        TNT1 A 1 //A_PlaySound("marathon/fist/hit")
        Stop
    }
}

actor MarathonKnifePuff : BulletPuff
{
    +CLIENTSIDEONLY
    +PUFFONACTORS
    //+NOBLOOD
    +NOEXTREMEDEATH
    Scale 0.25
    //ActiveSound "marathon/fist/hit"
    AttackSound "marathon/knife/wall"
    SeeSound "marathon/knife/hit"
    DamageType "Pistol"
    States
    {
      Crash:
        MPFF ABCDEFG 2 Bright
        stop
        
      Melee:
      Spawn:
        TNT1 A 1 //A_PlaySound("marathon/fist/hit")
        Stop
    }
}
